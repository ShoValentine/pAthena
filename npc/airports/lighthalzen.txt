//===== rAthena Script ======================================= 
//= Lighthalzen Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Lighthalzen Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

lhz_airport,143,43,5	script	Airport Staff#lhz_air1a::lhz_airport1	90,{
	mes "[Airport Staff]";
	mes "Selamat datang di";
	mes "Bandara Lighthalzen,";
	mes "tempat kami menawarkan nonstop";
	mes "penerbangan ke Einbroch, Juno dan Hugel.";
	next;
	if (select("Naik Kapal Udara.:Batal.") == 1) {
		mes "[Airport Staff]";
		mes "Biaya boarding adalah";
		mes "1.200 zeny, tapi bisa";
		mes "bebaskan biaya jika Anda menebus";
		mes "Tiket Gratis Pesawat.";
		next;
		if (select("Iya:Tidak") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "lhz_airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "lhz_airport",148,51;
				end;
			}
			mes "[Airship Staff]";
			mes "Maaf, tapi kamu tidak";
			mes "memiliki 1.200 zeny untuk membayar";
			mes "untuk biaya boarding.";
			close;
		}
	}
	mes "[Airport Staff]";
	mes "Terima kasih dan";
	mes "silakan datang lagi.";
	mes "Semoga harimu menyenangkan~";
	close;
}

lhz_airport,158,43,5	duplicate(lhz_airport1)	Airship Staff#lhz_air1b	90
lhz_airport,126,43,5	duplicate(lhz_airport1)	Airship Staff#lhz_air1c	90

lhz_airport,143,49,3	script	Arrival Staff#lhz_air2a::lhz_airport2	90,{
	mes "[Arrival Staff]";
	mes "Selamat datang di Bandara Lighthalzen.";
	mes "Tolong izinkan saya memandu Anda ke";
	mes "terminal utama jika Anda tiba dari penerbangan Anda. Jika tidak, silakan";
	mes "naik Kapal Udara yang berangkat untuk mencapai tujuan yang Anda tuju.";
	next;
	if (select("Keluar ke terminal utama.:Batal.") == 1) {
		mes "[Arrival Staff]";
		mes "Begitu Anda berada di terminal utama, Anda harus membayar biaya lagi";
		mes "untuk menaiki sebuah Kapal Udara. Anda harus";
		mes "hanya keluar jika Lighthalzen adalah tujuan yang Anda tuju. Bagaimana kalau kita";
		mes "lanjutkan ke terminal utama?";
		next;
		if (select("Iya:Tidak") == 1) {
			warp "lhz_airport",142,40;
			end;
		}
	}
	mes "[Arrival Staff]";
	mes "Baiklah, terima kasih";
	mes "untuk perlindungan Anda";
	mes "dan saya harap Anda punya";
	mes "penerbangan yang menyenangkan~";
	close;
}

lhz_airport,126,51,3	duplicate(lhz_airport2)	Arrival Staff#lhz_air2b	90
lhz_airport,158,50,3	duplicate(lhz_airport2)	Arrival Staff#lhz_air2c	90
