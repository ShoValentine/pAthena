lasa_sea	mapflag	tb
lasa_sea	mapflag	pvp
lasa_sea	mapflag	novending
lasa_sea	mapflag	nochat
lasa_sea	mapflag	noteleport
lasa_sea	mapflag	nowarp
lasa_sea	mapflag	nowarpto
lasa_sea	mapflag	nosave
lasa_sea	mapflag	nobranch
lasa_sea	mapflag	notomb

prontera,141,200,8	script	Boss Equipment	10007,{
	mes "๐ ต้องการ ^FF2929" + F_InsertComma(.req_zeny) + "z^000000 \nและ ^FF2929" + F_InsertComma(.req_item_amount) + " " + getitemname(.req_item_id) + "^000000 ในการเข้าห้อง";
	mes "---";
	mes "โทษภายในห้อง";
	mes "๐ ^FF2929เป็นห้อง PvP^000000";
	mes "๐ ^FF2929Damage จะเหลือ 5%^000000";
	mes "๐ ^FF2929Flee จะเหลือ 5%^000000";
	mes "๐ ^FF2929Perfect Dodge จะเหลือ 5%^000000";
	mes "๐ ^FF2929Monster จะโจมตีแรงขึ้น 5 เท่า^000000";
	mes "---";
	mes "รางวัล";
	mes "๐ ^3629FFเมื่อกำจัด Boss จะได้รับ Equipment ที่ต้องใส่ตอน Level 99 ขึ้นไป^000000";
	next;
	menu "เข้า",-;
	// Check Zeny
	if (Zeny < .req_zeny){
		mes "Zeny ไม่เพียงพอ";
		close;
	}
	// Check Item
	if (countitem(.req_item_id) < .req_item_amount){
		mes getitemname(.req_item_id) + " ไม่เพียงพอ";
		close;
	}
	Zeny -= .req_zeny;
	delitem .req_item_id,.req_item_amount;
	specialeffect2 EF_MAXPOWER;
	setpcblock PCBLOCK_ALL,true;
	setpcblock PCBLOCK_MOVE,false;
	addtimer 2000,strnpcinfo(3) + "::OnWarp";
	announce strcharinfo(0) + " เข้าห้อง Boss (Equipment)",bc_all;
	warp .map_name$,0,0;
	end;
	
OnWarp:
	setpcblock PCBLOCK_ALL,false;
	end;
	
OnCleanUp:
	.no_respawn = 1;
	killmonsterall .map_name$;
	callsub L_Spawn;
	.no_respawn = 0;
	end;
	
OnBossDead:
	if(.no_respawn)
	end;

	monster .map_name$,0,0,"--ja--",$all_boss_ids[rand(0,($all_boss_id_size - 1))],1,strnpcinfo(0) + "::OnBossDead";

	// Unexpected error
	if(!playerattached())
	end;

	// Get equipment
	freeloop(1);
	
	.@rand = rand(1,3);
	if(.@rand == 1){
		.@item_id = $weaponIds[rand(0,(getarraysize($weaponIds) - 1))];
		while(getiteminfo(.@item_id,ITEMINFO_EQUIPLEVELMIN) < 99)
		.@item_id = $weaponIds[rand(0,(getarraysize($weaponIds) - 1))];
	}
	else if(.@rand == 2){
		.@item_id = $equipmentIds[rand(0,(getarraysize($equipmentIds) - 1))];
		while(getiteminfo(.@item_id,ITEMINFO_EQUIPLEVELMIN) < 99)
		.@item_id = $equipmentIds[rand(0,(getarraysize($equipmentIds) - 1))];
	}
	else if(.@rand == 3){
		.@item_id = $costumeIds[rand(0,(getarraysize($costumeIds) - 1))];
		while(getiteminfo(.@item_id,ITEMINFO_EQUIPLEVELMIN) < 99)
		.@item_id = $costumeIds[rand(0,(getarraysize($costumeIds) - 1))];
	}
	
	freeloop(0);
	
	announce strcharinfo(0) + " ได้รับ " + getitemname(.@item_id),bc_map;
	
	getitem .@item_id,1;
	
	end;
	
	L_Spawn:
	for(.@i = 0; .@i < .spawn_amount; .@i++)
	monster .map_name$,0,0,"--ja--",$all_boss_ids[rand(0,($all_boss_id_size - 1))],1,strnpcinfo(0) + "::OnBossDead";
	return;
	
OnInit:
	.map_name$ = "lasa_sea";
	
	.req_zeny = 900000;
	.req_item_id = 40017;
	.req_item_amount = 9;
	
	.spawn_amount = 70;
	
	waitingroom "ห้อง Boss (Equipment)",0;
	
	callsub L_Spawn;
	
	end;
}
