//===== rAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.8 Added 3rd Job creature NPCs (Dragon/Gryphon) [L0ne_W0lf]
//= 1.9 Enabled Gryphon Renter NPC and added Mado Gear NPC. [Masao]
//= 2.0 Fixed problems with third classes and new mounts. [Euphy]
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Added warg checks. [Euphy]
//============================================================ 

// PecoPeco Breeder (for Knights) :: breeder
//============================================================
prontera,55,350,5	script	Peco Peco Breeder#knt	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[Peco Peco Breeder]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "ยินดีต้อนรับ.";
		mes "อัศวินผู้ทรงเกียรติ,";
		mes "ท่านต้องการเช่า";
		mes "Peco Peco ใช่หรือไหม?";
		mes "ค่าธรรมเนียมคือ "+.@price+" zeny.";
		next;
		switch(select("เช่า Peco Peco:ยกเลิก")) {
		case 1:
			if (Zeny < .@price) {
				mes "[Peco Peco Breeder]";
				mes "ดูเหมือนว่า";
				mes "ท่านมี zeny ไม่เพียงพอ.";
				mes "ท่าน...";
				mes "ล้มละลายหรือเปล่า?";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Peco Peco Breeder]";
				mes "ข้าขอโทษด้วย,";
				mes "แต่ท่านไม่มีสิทธิ์";
				mes "ในการใช้บริการนี้. โปรดไปเรียนรู้";
				mes "ทักษะสกิล Peco Peco Ride เสียก่อน.";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "ท่านอยู่ในระหว่าง";
				mes "ใช้บริการเช่า";
				mes "Peco Peco อยู่แล้ว.";
				close;
			}
			else if(ismounting()) {
				mes "[Peco Peco Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[Peco Peco Breeder]";
			mes "ข้าเข้าใจแล้ว.";
			mes "เอาล่ะ, ถ้าอย่างนั้น";
			mes "ขอให้ท่านมีวันที่ดีนะ.";
			close;
		}
	} else {
		mes "ข้าขอโทษ,";
		mes "แต่ Peco Pecos เหล่านี้";
		mes "มีไว้เฉพาะสำหรับ Knights";
		mes "และ Lord Knights เท่านั้น.";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)
//============================================================
prontera,232,318,3	script	Peco Peco Breeder#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[PecoPeco Breeder]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) mes "ยินดีต้อนรับ, Crusader.";
		else mes "ยินดีต้อนรับ, Paladin.";
		mes "เรามี Peco Peco";
		mes "พิเศษที่เตรียมไว้";
		mes "สำหรับท่าน. ค่าเช่า 1 ตัว";
		mes "คิดเป็นเงิน "+.@price+" zeny.";
		next;
		switch(select("เช่า PecoPeco:ยกเลิก")) {
		case 1:
			if (Zeny < .@price) {
				mes "[Peco Peco Breeder]";
				mes "ดูเหมือนว่า";
				mes "ท่านมี zeny ไม่เพียงพอ.";
				mes "หากท่านต้องการ";
				mes "Peco Peco โปรดนำ";
				mes " "+.@price+" zeny มา...";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[Peco Peco Breeder]";
				mes "ท่านต้องเรียนรู้ทักษะ";
				mes "การขี่ PecoPeco มาก่อน";
				mes "ก่อนที่ข้าจะให้ท่านเช่าได้.";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "ท่านอยู่ในระหว่าง";
				mes "เช่า Peco Peco อยู่แล้ว.";
				close;
			}
			else if(ismounting()) {
				mes "[Peco Peco Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			close;
		case 2:
			mes "[PecoPeco Breeder]";
			mes "แล้วพบกันใหม่.";
			close;
		}
	} else {
		mes "มีอะไรให้ข้าช่วยท่านหรือเปล่า?";
		mes "โปรดทราบด้วยว่า";
		mes "บริการเช่า Peco Peco นี้";
		mes "มีไว้ให้สำหรับ";
		mes "Crusaders และ Paladins เท่านั้น.";
		close;
	}
}

// Falcon Master :: breeder
//============================================================
hu_in01,381,304,5	script	Falcon Breeder#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[Falcon Breeder]";
	if (BaseJob == Job_Hunter) {
		if (checkwug()) {
			mes "อืม...";
			mes "เจ้าไม่สามารถเช่า Falcon ได้";
			mes "จนกว่าเจ้าจะปลด warg ของเจ้าออกก่อน!";
			close;
		}
		mes "เจ้าต้องการ Falcon หรือไม่?";
		mes "เจ้าสามารถเช่านกเหยี่ยว";
		mes "ของเจ้าเองได้";
		mes "ในราคาเพียง "+.@price+" zeny เท่านั้น~";
		next;
		switch(select("เช่า Falcon:ยกเลิก")) {
		case 1:
			if (checkwug()) {
				mes "[Falcon Breeder]";
				mes "อืม...";
				mes "เจ้าไม่สามารถเช่า Falcon ได้";
				mes "จนกว่าเจ้าจะปลด warg ของเจ้าออกก่อน!";
				close;
			}
			if (Zeny < .@price) {
				mes "[Falcon Breeder]";
				mes "นี่คืออะไร?";
				mes "เจ้ามี zeny";
				mes "ไม่เพียงพอหรือเปล่า?!";
				mes "เจ้าควรเริ่ม";
				mes "ตามล่าหาเงิน";
				mes "แทนที่จะเป็นมอนสเตอร์นะ~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Falcon Breeder]";
				mes "ให้ตายสิ~";
				mes "ไปเรียนรู้วิธีการ";
				mes "จัดการกับ Falcon";
				mes "ก่อนนะ! ข้าไม่สามารถ";
				mes "ให้เจ้าเช่าได้";
				mes "ถ้าเจ้าไม่สามารถจัดการกับมันได้, เจ้ารู้ใช่ไหม.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Falcon Breeder]";
				mes "อืม...";
				mes "เจ้ามี Falcon";
				mes "อยู่แล้ว. มันอยู่นั่น";
				mes "ตรงนั้น, เจ้าไม่เห็นมันหรอ?";
				close;
			}
			set Zeny, Zeny-.@price;
			setfalcon;
			close;
		case 2:
			mes "[Falcon Breeder]";
			mes "ระ-รอเดี๋ยวก่อน, เจ้ากำลัง";
			mes "จะไปไหน?";
			mes "Falcons พวกนี้เป็นระดับสุดยอด,";
			mes "ข้ารับประกันมันได้เลย! เอาน่า, โย่~";
			close;
		}
	} else {
		mes "เจ้าเด็กโง่!";
		mes "Falcons สามารถ";
		mes "ใช้ได้เฉพาะ Hunters";
		mes "และ Snipers เท่านั้นนะ, เข้าใจมั้ย?";
		mes "...เฮ้ เฮ้, อิจฉาหรือเปล่า?";
		close;
	}
}
