//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 2.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- รอสักครู่ !! -";
		mes "- ตอนนี้คุณกำลัง -";
		mes "- พกพาสิ่งของมากเกินไป. -";
		mes "- โปรดลองอีกครั้ง -";
		mes "- หลังจากที่คุณลดน้ำหนักลง. -";
		close;
	}
	mes "[Seiyablem]";
	mes "ข้าเป็นวิศวกรที่เชี่ยวชาญในการเพิ่ม Slots ให้กับ Weapons(อาวุธ) และ Armor(ชุดเกราะ) ปกติ.";
	mes "ค่าบริการของข้า, วัสดุที่ต้องใช้ และโอกาสในการสำเร็จทั้งหมดขึ้นอยู่กับอุปกรณ์เฉพาะที่ข้ากำลังทำงานอยู่.";
	next;
	mes "[Seiyablem]";
	mes "การเพิ่ม Slots อาจจะดูเหมือนง่าย, แต่มันซับซ้อนกว่าที่คิดและเห็นมาก.";
	mes "หากเจ้าสนใจในบริการของข้า, โปรดบอกข้า.";
	next;
	switch(select("เพิ่ม Slot ให้ Weapon (อาวุธ):เพิ่ม Slot ให้ Armor (ชุดเกราะ):ข้อมูลเพิ่มเติ่มในการเพิ่ม Slot:ยกเลิก"))
	{
		case 1:
			mes "[Seiyablem]";
			mes "เจ้าต้องการเพิ่ม Slot ให้ Weapon(อาวุธ) หรือไม่?";
			mes "ข้าจะแบ่งประเภทอาวุธตามความยากในการเพิ่ม Slot, โดยมี C Class นั้นง่ายที่สุด และ S Class นั้นยากที่สุด.";
			mes "เจ้าต้องการลองคลาสไหน?";
			next;
			switch(select("C Class (โอกาสสำเร็จ 25%):B Class (โอกาสสำเร็จ 20%):A Class (โอกาสสำเร็จ 15%):S Class (โอกาสสำเร็จ 10%)"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "เอ๊ะ C Class, ใช่ไหม?";
					mes "ตอนนี้, ข้าสามารถอัพเกรดอุปกรณ์บางอย่างในคลาสนั้นได้เท่านั้น, ดังนั้นโปรดเลือกรายการใดรายการหนึ่ง, เพื่อที่เราจะลองเพิ่ม Slot ให้กับมัน.";
					next;
					switch(select("Trident:Rope:Violin"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
					mes "B Class เหรอ?";
					mes "เอาล่ะ, นี่คือหมวดหมู่อุปกรณ์ระดับปานกลาง.";
					mes "โปรดเลือกอาวุธที่เจ้าต้องการให้ข้าลองเพิ่ม Slot.";
					next;
					switch(select("Chain:Gladius:Gakkung Bow:Pike:Haedonggum:Lute:Wire:Waghnakh:Arbalest Bow"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "โอ้ว, A Class นะ.";
					mes "เป็นพื้นที่ที่มีความเสี่ยงสูง!";
					mes "เอาล่ะ, แล้วอาวุธชิ้นไหนที่เจ้าอยากให้ข้าลองเพิ่ม Slot?";
					next;
					switch(select("Hunter Bow:Survivor's Rod(INT):Zweihander:Flamberge:Infiltrator:Ballista:Stunner:Berserk:Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					mes "ว้าว, ว้าว, ว้าว...";
					mes "S class เลยเหรอ? เอาล่ะ...";
					mes "คงจะเป็นเรื่องมหัศจรรย์มากหากข้าสามารถดึงสิ่งนี้ออกมาได้จริง.";
					mes "โอเค... เราจะลองเพิ่ม Slot เข้าไปในอาวุธไหนล่ะ?";
					next;
					switch(select("Gungnir:Poison Knife:Ice Pick:Sucsamad:Ginnungagap:Cutlas:Crescent Scythe:Survivor's Rod(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
			mes "เจ้าบอกว่า, ชุดเกราะใช่ไหม?";
			mes "แน่นอน, ไม่มีปัญหา.";
			mes "ชุดเกราะถูกแบ่งออกเป็น 4 คลาสที่แตกต่างกัน, ขึ้นอยู่กับความยากในการทำงาน.";
			mes "C Class นั้นง่ายที่สุด, และ S Class คือคลาสที่ยากที่สุด.";
			mes "เจ้าต้องการลองคลาสไหน?";
			next;
			switch(select("C Class (โอกาสสำเร็จ 25%):B Class (โอกาสสำเร็จ 20%):A Class (โอกาสสำเร็จ 15%):S Class (โอกาสสำเร็จ 10%)"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "เอ๊ะ C Class, ใช่ไหม?";
					mes "ฟังดูสมเหตุสมผลนะ.";
					mes "เจ้าต้องการให้ข้าลองเพิ่ม Slot ให้กับเกราะชิ้นไหน?";
					next;
					switch(select("Mantle:Coat:Circlet:Biretta"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes "เจ้าได้เลือก B Class ระดับปานกลาง.";
					mes "แล้ว, เจ้ามีชุดเกราะแบบไหนล่ะ?";
					next;
					switch(select("Mirror Shield:Chain Mail:Saint Robe:Silk Robe:Boots:Shoes:Muffler:Guard:Buckler:Shield:Bongun Hat"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "โอ้ว, A Class นะ.";
					mes "เป็นพื้นที่ที่มีความเสี่ยงสูง!";
					mes "เอาล่ะ, แล้วชุดเกราะชิ้นไหนที่เจ้าอยากให้ข้าลองเพิ่ม Slot?";
					next;
					switch(select("Gemmed Sallet:Bucket Hat:Memory Book:Tights:Legion Plate Armor:Full Plate:Thief Clothes:Greaves:Coif:Manteau:Helm:Ninja Suit:Orc Helm:Ancient Cape:Monk Hat:Golden Gear:Brooch:Munak Hat"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes "อืม... S Class?";
					mes "เจ้าแน่ใจเกี่ยวกับเรื่องนี้หรือเปล่า?";
					mes "เอาล่ะ... บอกชุดเกราะที่เจ้าต้องการเพิ่ม Slot มาให้ข้า, แล้วข้าจะบอกว่าต้องใช้อะไรบ้างเพื่อลองทำมัน.";
					next;
					switch(select("Majestic Goat:Spiky Band:Bone Helm:Corsair:Crown:Tiara:Sphinx Hat:Robe of Cast:Earring:Ring:Bow Thimble"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes "เอาล่ะ, ข้ายังไม่ได้ขัดเกลาศิลปะของ Slot Addition เลย.";
			mes "มันซับซ้อนมากจนข้าต้องโกหกถ้าข้าบอกเจ้าว่าข้ารู้ทุกปัจจัยที่ส่งผลต่อกระบวนการนี้.";
			mes "ถึงกระนั้น, ข้าสังเกตเห็นแนวโน้มบางอย่าง...";
			next;
			mes "[Seiyablem]";
			mes "ในบางเมือง, การเพิ่ม Slot จะประสบความสำเร็จมากกว่าสำหรับอุปกรณ์ที่มีการอัพเกรดน้อยกว่า.";
			mes "ในเมืองอื่น ๆ, สิ่งที่ตรงกันข้ามคือความจริง.";
			mes "มันไม่ใช่เรื่องแปลกจริงๆเหรอ?";
			next;
			mes "[Seiyablem]";
			mes "โอ้, ยังมีบางสิ่งที่สำคัญมากที่เจ้าควรรู้.";
			mes "ถ้าเจ้าขอให้ข้าเพิ่ม Slot เสริมให้กับอะไรสักอย่าง, ตรวจสอบให้แน่ใจว่าเจ้าไม่ได้มีสิ่งนั้นหลายรายการในกระเป๋าของเจ้า.";
			next;
			mes "[Seiyablem]";
			mes "นี่คือตัวอย่าง: หากเจ้ามี +7 Manteau และ Manteau ปกติอยู่ในกระเป๋าของเจ้า, ข้าจะสุ่มเลือกหนึ่งในนั้นเพื่อทำการเพิ่ม Slot ให้.";
			mes "แค่จำไว้ว่าต้องระวัง.";
			next;
			mes "[Seiyablem]";
			mes "ย้ำอีกครั้ง, ^FF0000ให้พกเพียงอุปกรณ์ชิ้นเดียวที่เจ้าต้องการให้ข้าเพิ่ม Slot^000000.";
			mes "อุปกรณ์อื่นๆ ทั้งหมดที่มีชื่อเดียวกัน ควรถูกเก็บไว้ใน Kafra Storage ของเจ้า, เข้าใจไหม?";
			close;
		case 4:
			mes "[Seiyablem]";
			mes "ใจเย็นๆ นะ, นักผจญภัย.";
			mes "ถ้าเจ้าต้องการที่จะลองเพิ่ม Slot ให้กับอุปกรณ์ของเจ้า, กลับมาบอกข้าได้เลย.";
			mes "แล้วเจอกันนะ~";
			close;
	}
}

function	script	Func_Socket	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Seiyablem]";
	mes "เจ้าต้องการเพิ่ม Slot ให้กับ "+ getitemname(.@item_id) +" ใช่ไหม?";
	mes "เอาล่ะ, โปรดนำ ^FF0000"+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +"^000000, "+ (.@item_req[1] != 0 && .@req_amount[1] != 0 ? ("^FF0000"+ .@req_amount[1] +" "+ getitemname(.@item_req[1]) + (.@req_amount[1] > 1 && .@item_req[1] != 999 ? "s":"") + "^000000 ") : "") + "และ ค่าบริการ ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny^000000 มาให้ข้า.";
	mes "อ๊า, และอย่าลืมนำ "+ getitemname(.@item_id) +" มาด้วยนะ!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(.@item_id, ITEMINFO_LOCATIONS) & EQP_HAND_R) // weapon
	{
		mes "ข้าสามารถลองเพิ่ม Slot ได้ตอนนี้ถ้าเจ้ามีไอเทมที่ต้องการและ zeny.";
		mes "อย่างไรก็ตาม, เจ้าควรรู้ว่ามีโอกาสที่ข้าจะล้มเหลวได้.";
		mes "ดังนั้น, ข้าจึงต้องแจ้งเตือนเจ้าไว้ด้วย...";
		next;
		mes "[Seiyablem]";
		mes "หากความพยายามในการเพิ่ม Slot ให้กับอาวุธของเจ้าล้มเหลว, นั้นหมายความว่า ^FF0000Weapon(อาวุธ)^000000, ^FF0000และ Cards ใดๆ ที่ประกอบเข้าด้วยกันจะถูกทำลาย^000000.";
	}
	else // armor
	{
		mes "ถ้าเจ้ามีวัตถุดิบที่จำเป็นทั้งหมด, ค่าบริการ zeny ของข้า และชุดเกราะ, เราก็สามารถดำเนินการเพิ่ม Slot ได้เลย.";
		mes "แต่ก่อนหน้านั้น, ข้าต้องเตือนเจ้าถึงความเสี่ยง.";
		next;
		mes "[Seiyablem]";
		mes "หากความพยายามในการเพิ่ม Slot ให้กับชุดเกราะของเจ้าล้มเหลว, นั้นหมายความว่า ^FF0000Armor(ชุดเกราะ)^000000, ^FF0000การอัพเกรดของมัน^000000 ^FF0000และ Cards ใดๆ ที่ประกอบเข้าด้วยกันจะถูกทำลาย^000000.";
	}
	mes "เจ้ายังต้องการลองเพิ่ม Slot อยู่หรือเปล่า?";
	next;
	switch(select("ยืนยันการสร้าง Slot:ยกเลิก"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes "เอาล่ะ, ถ้างั้นเริ่มงานได้เลย!";
				mes "เจ้าควรอธิษฐานขอให้ประสบความสำเร็จ.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes "เยี่ยมมาก, ดูเหมือนว่าจะประสบความสำเร็จ.";
					mes "ดูเหมือนจะทำได้อย่างดีเยี่ยม. ยินดีด้วย!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes "แล้วพบกันใหม่, นะเพื่อน!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes "ว้าาา! ...ข้าขอโทษ, มันล้มเหลว.";
					mes "อย่างไรก็ตาม, ข้าไม่ได้ทำผิดพลาดใดๆ เลย.";
					mes "นี่คือโชคของเจ้า, และมันถูกลิขิตโดยพระเจ้า, โอเคไหม?";
					mes "อย่าผิดหวังมากนัก, และลองครั้งต่อไป.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Seiyablem]";
					mes "ข้าขอให้เจ้าโชคดีในครั้งต่อไป!";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes "ข้าอยากจะดำเนินการต่อด้วยการเพิ่ม Slot นี้, แต่เจ้ายังขาดบางสิ่งบางอย่างไป.";
				mes "เจ้าแน่ใจว่าไหมว่าเจ้ามีอุปกรณ์, วัตถุดิบที่จำเป็น และ zeny ครบแล้วใช่ไหม?";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes "ต้องการเวลาคิดเกี่ยวกับมัน, ใช่ไหม?";
			mes "เอาล่ะ, ข้าเข้าใจแล้ว.";
			mes "แค่จำไว้ว่าชีวิตมันไม่สนุกเลย ถ้าเจ้าใช้ชีวิตอย่างปลอดภัยอยู่เสมอ~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	84
morocc,51,41,4	duplicate(SocketEnchant)	Seiyablem#moc	84

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[Young Man]";
	mes "ข้ากำลังพิจารณาที่จะเป็นช่างเทคนิค Slotting.";
	mes "มันเป็นสาขาใหม่ที่เจ้าสามารถเพิ่ม Slots ให้กับอุปกรณ์ได้, และมันน่าสนใจมากถ้าข้าสามารถทำงานในอาชีพที่มีรายได้ดีอย่างนี้ได้.";
	next;
	mes "[Young Man]";
	mes "มีองค์ประกอบใหญ่ของโชคที่เกี่ยวข้องกับการเพิ่ม Slots ได้สำเร็จจากสิ่งที่ข้าเข้าใจ.";
	mes "บางครั้งเจ้าอาจล้มเหลว, แต่ถ้าเจ้าโชคดีมาก, เจ้าอาจเพิ่ม Slots ได้สองช่อง.";
	mes "บ้าไปแล้ว. ใช่ไหมล่ะ?";
	next;
	mes "[Young Man]";
	mes "แต่อย่าตื่นเต้นมากเกินไป.";
	mes "มีเพียงอุปกรณ์บางอย่างเท่านั้นที่สามารถรองรับการเพิ่ม Slots พิเศษสองช่องได้.";
	mes "ยังมีอีกมากที่ข้าต้องเรียนรู้ก่อนที่จะสามารถเป็นเด็กฝึกงานได้...";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Young Man#prt	97
payon,143,143,7	duplicate(SocketMan)	Young Man#pay	97
morocc,60,42,3	duplicate(SocketMan)	Young Man#moc	97
lhz_in02,269,33,5	duplicate(SocketMan)	Young Man#lhz	97
