//===== rAthena Script =======================================
//= Old Pharmacist
//===== By: ==================================================
//= DZeroX
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Trade in items for potions.
//= Breakdown of arguments used:
//= arg(0): Herb required.
//= arg(1): Zeny cost.
//= arg(2): Potion given.
//===== Additional Comments: =================================
//= 1.0 Fully working. [DZeroX]
//= 1.0a Minor fixes [Lupus]
//= 1.1 Added changed the dialogs and name to fit the
//=     the official information. [DZeroX]
//= 1.1a Removed .GATs [Lupus]
//= 1.1b Fixed a bug in Orange Potion creating. Copy&Pasting = bad. [L0ne_W0lf]
//= 1.2 Added checkweight, and input min/max values. [L0ne_W0lf]
//= 1.3 Fixed minor condition check bug. (bugreport:597) [L0ne_W0lf]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//============================================================

alberta_in,16,28,4	script	Pharmacist	61,{
	if (checkweight(1201,1) == 0) {
		mes "- รอสักครู่! -";
		mes "- ตอนนี้คุณกำลัง -";
		mes "- พกพาสิ่งของมากเกินไป. -";
		mes "- โปรดกลับมาใหม่อีกครั้ง -";
		mes "- หลังจากที่คุณเก็บบางรายการไว้ใน kafra storage. -";
		close;
	}
	mes "[Old Pharmacist]";
	mes "อืมมมม...";
	mes "อะไรทำให้เจ้ามาที่นี่...?";
	next;
	switch(select("ทำ Potion:พูดคุย:ข้อมูลของส่วนผสม:ยกเลิก")) {
	case 1:
		if (MaxWeight - Weight < 5000) {
			mes "[Old Pharmacist]";
			mes "ทำไมเจ้าถึงแบกของพวกนี้ไปเยอะขนาดนี้!";
			mes "อย่าโลภ, พกไปเท่าที่จำเป็นเท่านั้น!";
			next;
			mes "[Old Pharmacist]";
			mes "เ้จาหนักเกินไปที่จะรับยาจากข้าได้...";
			mes "ไปเก็บของบางรายการไว้ในคลังของเจ้าก่อน!";
			close;
		} else {
			mes "[Old Pharmacist]";
			mes "เจ้ามีวัสดุทั้งหมดพร้อมแล้ว, ใช่ไหม? เจ้าต้องการอันไหน?";
			next;
			switch(select("Red Potion.:Orange Potion.:Yellow Potion.:White Potion.:Blue Potion.:Green Potion.:จริงๆแล้ว, ข้าไม่ต้องการสิ่งใดเลย")) {
			case 1:
				callsub L_Making,507,3,501;
			case 2:
				mes "[Old Pharmacist]";
				if (countitem(507) < countitem(713)) {
					set .@max,countitem(507);
				}
				else if (countitem(508) < countitem(713)) {
					set .@max,countitem(508);
				}
				else {
					set .@max,countitem(713);
				}
				if ((countitem(507) < 1) || (countitem(508) < 1) || (countitem(713) == 0)) {
					mes "เจ้าเด็กบ้า! เจ้าคาดหวังอะไรอยู่?! มาที่นี่โดยไม่มีอะไรเลย. Tsk!";
					mes "ไปให้พ้น!";
					close;
				}
				if (Zeny < 3) {
					mes "เจ้าเด็กบ้า! เจ้าคาดหวังอะไรอยู่?! มาที่นี่โดยไม่มีอะไรเลย. Tsk!";
					mes "ไปให้พ้น!";
					close;
				}
				mes "เท่าไหร่?";
				next;
				switch(select("ทํามากที่สุดเท่าที่จะทําได้:ข้าต้องการเลือกจำนวน:จริงๆแล้ว, ข้าไม่ต้องการสิ่งใดเลย")) {
				case 1:
					if ((countitem(507) < .@max) || (countitem(508) < .@max) || (countitem(713) < .@max) || (Zeny < .@max*3)) {
						mes "[Old Pharmacist]";
						mes "เจ้าเด็กบ้า! เจ้ามีวัตถุดิบไม่ครบทั้งหมดและเจ้ายังต้องการให้ข้าทำยาอีกให้เหรอ?!";
						close;
					}
					set Zeny,Zeny-(.@max*5);
					delitem 507,.@max; //Red_Herb
					delitem 508,.@max; //Yellow_Herb
					delitem 713,.@max; //Empty_Bottle
					getitem 502,.@max; //Orange_Potion
					break;
				case 2:
					mes "[Old Pharmacist]";
					mes "จากนั้นเลือกตัวเลขที่ต่ำกว่า 100. ถ้าเจ้าไม่ต้องการมัน, เพียงแค่ใส่ '0'. ด้วยวัสดุที่เจ้ามี, เจ้าสามารถทำยาได้ประมาณ "+.@max+" ขวด.";
					next;
					input .@amount,0,101;
					if (.@amount == 0) {
						mes "[Old Pharmacist]";
						mes "เจ้าช่วย, ตัดสินใจสิ?!";
						close;
					}
					if (.@amount > 100) {
						mes "[Old Pharmacist]";
						mes "เจ้าหูหนวกหรือเปล่า? ข้าบอกว่าน้อยกว่า 100!";
						close;
					}
					if ((countitem(507) < .@amount) || (countitem(508) < .@amount) || (countitem(713) < .@amount) || (Zeny < .@amount*3)) {
						mes "[Old Pharmacist]";
						mes "เจ้าเด็กบ้า! เจ้ามีวัตถุดิบไม่ครบทั้งหมดและเจ้ายังต้องการให้ข้าทำยาอีกให้เหรอ?!";
						close;
					}
					next;
					set Zeny,Zeny-(.@amount*5);
					delitem 507,.@amount; //Red_Herb
					delitem 508,.@amount; //Yellow_Herb
					delitem 713,.@amount; //Empty_Bottle
					getitem 502,.@amount; //Orange_Potion
					break;
				case 3:
					mes "[Old Pharmacist]";
					mes "อะไรนะ?!";
					mes "Grrr...";
					mes "Bleh!";
					mes "ไปให้พ้น!";
					close;
				}
				mes "[Old Pharmacist]";
				mes "นี่เจ้าเอาไปสิ. ทุกอย่างเสร็จแล้ว เจ้าสามารถเอามันไปได้เลย. แต่จำไว้! การใช้ยาในทางที่ผิดนั้นไม่ดี.";
				close;
			case 3:
				callsub L_Making,508,10,503;
			case 4:
				callsub L_Making,509,20,504;
			case 5:
				callsub L_Making,510,30,505;
			case 6:
				callsub L_Making,511,3,506;
			case 7:
				mes "[Old Pharmacist]";
				mes "อะไรนะ?!";
				mes "Grrr...";
				mes "Bleh!";
				mes "ไปให้พ้น!";
				close;
			}
		}
	case 2:
		mes "[Old Pharmacist]";
		mes "ด้วยยา, เจ้าสามารถเพิ่มความสามารถในการฟื้นฟูของบุคคลได้. แต่, พวกมันดีในระดับหนึ่งเท่านั้น. *Sigh* ข้าเริ่มคิดถึงสมัยที่ข้ายังเป็นเด็ก. ข้าคงจะแก่แล้วสินะ.";
		next;
		mes "[Old Pharmacist]";
		mes "อย่างไรก็ตาม, ยาก็เป็นเพียงยา. ไม่มีอะไรมากหรือน้อยไปกว่านั้น.";
		close;
	case 3:
		mes "[Old Pharmacist]";
		mes "ฮึ่มมมมม...";
		mes "พวกเจ้าอาจจะค่อนข้างน่ารำคาญ. แต่, ในเมื่อเจ้าถาม, ข้าจะอธิบายให้.";
		next;
		mes "[Old Pharmacist]";
		mes "สมุนไพรใช้ได้ผลดีในตัวของมันเอง, แต่ถ้าเจ้าใช้เทคนิคและทักษะพิเศษของข้าในการทำยาจากสมุนไพรเหล่านั้น, ผลลัพธ์ก็จะดีขึ้นมากมาย.";
		next;
		mes "[Old Pharmacist]";
		mes "ถ้าเจ้าถามอย่างกระตือรือร้นและสุภาพ, ข้าก็จะทำให้เจ้าได้. แต่, จะไม่ได้ฟรีนะ... อย่ากังวลไป, ข้าคิดค่าบริการเพียงเล็กน้อยเท่านั้น, ดังนั้นมันจึงไม่แพงขนาดนั้น.";
		next;
		mes "[Old Pharmacist]";
		mes "Red Potion - ^0098E52 Red Herbs, 1 Empty Bottle, ค่าบริการ 2 zeny.^000000";
		mes "Orange Potion - ^0098E51 Red Herb, 1 Yellow Herb, 1 Empty Bottle, ค่าบริการ 5 zeny.^000000";
		mes "Yellow Potion - ^0098E52 Yellow Herbs, 1 Empty Bottle, ค่าบริการ 10 zeny.^000000";
		next;
		mes "[Old Pharmacist]";
		mes "White Potion - ^0098E52 White Herbs, 1 Empty Bottle, ค่าบริการ 20 zeny.^000000";
		mes "Blue Potion - ^0098E52 Blue Herbs, 1 Empty Bottle, ค่าบริการ 30 zeny.^000000";
		mes "Green Potion - ^0098E52 Green Herbs, 1 Empty Bottle, ค่าบริการ 3 zeny.^000000";
		close;
	case 4:
		mes "[Old Pharmacist]";
		mes "ช่างเป็นคนน่าเบื่อจริงๆ. หากเจ้ามีอะไรจะพูด, ทำไมไม่พูดมันออกมาล่ะ?!";
		close;
	}
	
L_Making:
	.@item_req = getarg(0);
	.@req_amount = getarg(1);

	mes "[Old Pharmacist]";
	if ((countitem(.@item_req)/2) < countitem(713)) {
		.@max = countitem(.@item_req) /2;
	} else {
		.@max = countitem(713);
	}
	if (countitem(.@item_req) < 2 || countitem(713) == 0) {
		mes "เจ้าเด็กบ้า! เจ้าคาดหวังอะไรอยู่?! มาที่นี่โดยไม่มีอะไรเลย. Tsk!";
		mes "ไปให้พ้น!";
		close;
	}
	if (Zeny < 3) {
		mes "เจ้าเด็กบ้า! เจ้าคาดหวังอะไรอยู่?! มาที่นี่โดยไม่มีอะไรเลย. Tsk!";
		mes "ไปให้พ้น!";
		close;
	}
	mes "เท่าไหร่?";
	next;
	switch(select("ทํามากที่สุดเท่าที่จะทําได้:ข้าต้องการเลือกจำนวน:จริงๆแล้ว, ข้าไม่ต้องการสิ่งใดเลย")) {
	case 1:
		if ((countitem(.@item_req) < .@max*2) || countitem(713) < .@max || (Zeny < .@max*.@req_amount)) {
			mes "[Old Pharmacist]";
			mes "เจ้าเด็กบ้า! เจ้ามีวัตถุดิบไม่ครบทั้งหมดและเจ้ายังต้องการให้ข้าทำยาอีกให้เหรอ?!";
			close;
		}
		Zeny = Zeny - (.@max*.@req_amount);
		delitem .@item_req,.@max*2;
		delitem 713,.@max; //Empty_Bottle
		getitem getarg(2),.@max; 
		break;
	case 2:
		mes "[Old Pharmacist]";
		mes "จากนั้นเลือกตัวเลขที่ต่ำกว่า 100. ถ้าเจ้าไม่ต้องการมัน, เพียงแค่ใส่ '0'. ด้วยวัสดุที่เจ้ามี, เจ้าสามารถทำยาได้ประมาณ "+.@max+" ขวด.";
		next;
		input .@amount;
		if (.@amount == 0) {
			mes "[Old Pharmacist]";
			mes "เจ้าช่วย, ตัดสินใจสิ?!";
			close;
		}
		if (.@amount > 100) {
			mes "[Old Pharmacist]";
			mes "เจ้าหูหนวกหรือเปล่า? ข้าบอกว่าน้อยกว่า 100!";
			close;
		}
		if ((countitem(.@item_req) < .@amount*2) || countitem(713) < .@amount || (Zeny < .@amount*.@req_amount)) {
			mes "[Old Pharmacist]";
			mes "เจ้าเด็กบ้า! เจ้ามีวัตถุดิบไม่ครบทั้งหมดและเจ้ายังต้องการให้ข้าทำยาอีกให้เหรอ?!";
			close;
		}
		Zeny = Zeny - (.@max*.@req_amount);
		delitem .@item_req,.@amount*2;
		delitem 713,.@amount; //Empty_Bottle
		getitem getarg(2),.@amount; 
		break;
	case 3:
		mes "[Old Pharmacist]";
		mes "อะไรนะ?!";
		mes "Grrr...";
		mes "Bleh!";
		mes "ไปให้พ้น!";
		close;
	}
	mes "[Old Pharmacist]";
	mes "นี่เจ้าเอาไปสิ. ทุกอย่างเสร็จแล้ว เจ้าสามารถเอามันไปได้เลย. แต่จำไว้! การใช้ยาในทางที่ผิดนั้นไม่ดี.";
	close;
}
