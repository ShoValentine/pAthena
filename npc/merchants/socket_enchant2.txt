//===== rAthena Script ======================================= 
//= Episode 12 Socket Enchant NPC
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 1.1a Added 'disable_items' command. [Euphy]
//============================================================ 

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- รอสักครู่ !! -";
		mes "- ตอนนี้คุณกำลัง -";
		mes "- พกพาสิ่งของมากเกินไป. -";
		mes "- โปรดลองอีกครั้ง -";
		mes "- หลังจากที่คุณลดน้ำหนักลง. -";
		close;
	}
	mes "[Leablem]";
	mes "สวัสดี. ชื่อของข้าคือ Leablem,";
	mes "และข้าก็เป็นช่างเทคนิคที่เชี่ยวชาญ";
	mes "เรื่องการเพิ่ม slots ให้กับอาวุธและชุดเกราะ.";
	mes "บางครั้งมันอาจเป็นเรื่องยากมาก, แต่ข้าชอบ";
	mes "สิ่งที่ข้าทำ, และภาคภูมิใจในสิ่งนั้น.";
	next;
	mes "[Leablem]";
	mes "ข้าได้เรียนรู้สิ่งต่าง ๆ เกือบทั้งหมดจากพี่ชายของข้า, Seyablem,";
	mes "แต่ข้าคิดว่าข้ายังต้องเรียนรู้อีกมาก.";
	mes "ดังนั้นโปรดเข้าใจว่าบริการเพิ่ม slotting ของข้ามีข้อจำกัด,";
	mes "ไม่เหมือนกับพี่ชายของข้า Seyablem.";
	next;
	mes "[Leablem]";
	mes "พี่ชายของข้าไม่ค่อยมีความสุขที่จะสอน";
	mes "ทักษะของเขาแก่ข้า เพราะเขากังวลว่าทักษะของเขา";
	mes "จะไม่มีเอกลักษณ์อีกต่อไป. อย่างไรก็ตาม, เขาทำดีที่สุดแล้ว,";
	mes "ที่จะสอนข้า.";
	next;
	mes "[Leablem]";
	mes "ด้วยความกรุณาของเขา, ข้าได้เรียนรู้หลายสิ่งหลายอย่าง.";
	mes "ข้าไม่ค่อยมั่นใจมากนัก";
	mes "ในการลองสิ่งต่างๆ มากมาย,";
	mes "แต่ข้าจะทำสิ่งที่";
	mes "ข้าสามารถทำได้ในตอนนี้.";
	next;
	mes "[Leablem]";
	mes "อันที่จริง, มีสิ่งหนึ่งที่";
	mes "ที่พี่ชายของข้าไม่อยากให้ข้าทำ.";
	mes "ข้าไม่อยากทำให้เขาเสียใจ, ดังนั้น...ข้ามส่วนนั้นไปเถอะ.";
	mes "ข้าจะไม่พูดอะไรอีกเกี่ยวกับมัน!";
	next;
	mes "[Leablem]";
	mes "อย่างไรก็ตาม, เจ้ามีอุปกรณ์ใด";
	mes "ที่เจ้าต้องการเพิ่ม slots หรือไม่?";
	mes "ค่าบริการของข้า, วัสดุที่จำเป็น,";
	mes "และโอกาสสำเร็จทั้งหมดขึ้นอยู่กับ";
	mes "รายการเฉพาะที่ข้ากำลังทำอยู่.";
	next;
	switch(select("Weapon (อาวุธ):Armor (ชุดเกราะ):เกี่ยวกับเรื่องนั้นที่ท่านข้ามไป:ข้อมูลเพิ่มเติม:ยกเลิก"))
	{
		case 1:
			mes "[Leablem]";
			mes "อาวุธ, เจ้าพูดเหรอ? แน่นอน, ไม่มีปัญหา.";
			mes "อาวุธถูกแบ่งออกเป็น 4 ประเภทที่แตกต่างกัน";
			mes "ขึ้นอยู่กับความยากในการทำงาน.";
			mes "คลาส C นั้นเป็นคลาสที่ง่ายที่สุด และคลาส S นั้นยากที่สุด.";
			mes "แล้วเจ้าอยากจะลองคลาสไหน?";
			next;
			switch(select("C (โอกาสสำเร็จ 25%):B (โอกาสสำเร็จ 20%):A (โอกาสสำเร็จ 15%):S (โอกาสสำเร็จ 10%)"))
			{
				case 1:
					mes "[Leablem]";
					mes "คลาส C, ข้าเข้าใจแล้ว.";
					mes "แล้ว, เจ้ามีอาวุธแบบไหนล่ะ?";
					next;
					switch(select("Book of Mother Earth:Book of Billows:Book of Gust of Wind:Book of the Blazing Sun"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[Leablem]";
					mes "เจ้าได้เลือกคลาส B ระดับปานกลาง.";
					mes "แล้ว, เจ้ามีอาวุธแบบไหนล่ะ?";
					next;
					switch(select("Orcish Axe:Scimiter:Spike"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[Leablem]";
					mes "คลาส A ใช่ไหม? ดูเหมือนว่าเจ้าต้องการมากเกินไปนิดหน่อย. แต่ก็, ไม่มีปัญหา.";
					mes "แล้ว, เจ้ามีอาวุธแบบไหนล่ะ?";
					next;
					switch(select("Dragon Killer:Katar of Quaking:Katar of Raging Blaze:Katar of Frozen Icicle:Katar of Piercing Wind:Golden Mace:Oriental Lute:Queen's Whip:Spectral Spear:Gae Bolg:Schweizersabel"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[Leablem]";
					mes "ห๊ะ, คลาส S เหรอ?! โอ้พระเจ้า, เอาจริงหรือเปล่า?*ตัวสั่น*";
					mes "ถ้าอย่างนั้น...อาวุธคลาส S แบบไหนที่เจ้าอยากจะสร้าง slot เพิ่ม?!";
					next;
					switch(select("Zephyrus:Mailbreaker:Dragon Slayer:Swordbreaker:Assasin Dagger:Grand Cross:Executioner"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[Leablem]";
			mes "ชุดเกราะ, เจ้าพูดเหรอ? แน่นอน, ไม่มีปัญหา.";
			mes "ชุดเกราะถูกแบ่งออกเป็น 4 ประเภทที่แตกต่างกัน";
			mes "ขึ้นอยู่กับความยากในการทำงาน.";
			mes "คลาส C นั้นเป็นคลาสที่ง่ายที่สุด และคลาส S นั้นยากที่สุด.";
			mes "แล้วเจ้าอยากจะลองคลาสไหน?";
			next;
			switch(select("C (โอกาสสำเร็จ 25%):B (โอกาสสำเร็จ 20%):A (โอกาสสำเร็จ 15%):S (โอกาสสำเร็จ 10%)"))
			{
				case 1:
					mes "[Leablem]";
					mes "คลาส C, ข้าเข้าใจแล้ว.";
					mes "แล้ว, เจ้ามีชุดเกราะแบบไหนล่ะ?";
					next;
					switch(select("Sunflower:Ph.D Hat:Big Ribbon:Boys Cap"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[Leablem]";
					mes "เจ้าได้เลือกคลาส B ระดับปานกลาง.";
					mes "แล้ว, เจ้ามีชุดเกราะแบบไหนล่ะ?";
					next;
					switch(select("Skull Ring:High Heels"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[Leablem]";
					mes "คลาส A ใช่ไหม? ดูเหมือนว่าเจ้าต้องการมากเกินไปนิดหน่อย. แต่ก็, ไม่มีปัญหา.";
					mes "แล้ว, เจ้ามีชุดเกราะแบบไหนล่ะ?";
					next;
					switch(select("Pirate Bandana:Black Leather Boots"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
						//case 3: callfunc "Func_Socket2",15053,15056,x,x,x,x,x;	// Special Ninja Suit (Needs more info)
					}
				case 4:
					mes "[Leablem]";
					mes "ห๊ะ, คลาส S เหรอ?! โอ้พระเจ้า, เอาจริงหรือเปล่า?*ตัวสั่น*";
					mes "ถ้าอย่างนั้น...ชุดเกราะคลาส S แบบไหนที่เจ้าอยากจะสร้าง slot เพิ่ม?!";
					next;
					switch(select("Mage Coat:Holy Robe:Sacred Mission:Undershirt:Pantie"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[Leablem]";
			mes "โอ้, มันไม่มีอะไรพิเศษ, แต่เขาคิดว่า";
			mes "มันไม่ใช่สิ่งที่มนุษย์จะมายุ่งวุ่นวายด้วย. ";
			mes "ข้าไม่อยากทำให้พี่ชายข้าผิดหวัง,";
			mes "เพราะฉะนั้นข้าควรจะลืมมันซะดีกว่า.";
			next;
			mes "[Leablem]";
			mes "...บริการนั้นมีโอกาสสำเร็จสูงมาก,";
			mes "แต่ต้องใช้วัสดุจำนวนมากและค่าบริการอัตราสูง.";
			mes "ข้าเชื่อว่าไม่มีใครจะต้องการใช้บริการ";
			mes "นั้นจริง ๆ, ถึงแม้ว่าข้าจะเปิดมันก็ตาม.";
			next;
			mes "[Leablem]";
			mes "...อืมมม...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes "ดูเหมือนเจ้าจะสนใจที่จะใช้บริการนั้น.";
			mes "ถ้าเจ้าต้องการ, ข้าสามารถบอกเจ้าเกี่ยวกับมันได้.";
			mes "แต่เจ้าต้องสัญญาว่าจะเก็บเรื่องนี้ไว้เป็นความลับ.";
			mes "ถ้าพี่ชายของข้ารู้ว่าข้ากำลังจะบอกอะไรเจ้า,";
			mes "เขาคงจะตีข้าแน่!";
			next;
			mes "[Leablem]";
			mes "บริการพิเศษนี้มีโอกาสสำเร็จถึง 90%,";
			mes "แต่ต้องการ 2 Gold และ 200,000,000 zeny:";
			mes "ข้าสามารถเพิ่ม slots ให้กับ Hat of the Sun God ได้.";
			next;
			mes "[Leablem]";
			mes "พี่ชายของข้าบอกว่า, ตั้งแต่สมัยโบราณ, การปรับเปลี่ยนหมวกใบนั้นถือเป็นสิ่งต้องห้าม.";
			mes "แต่ขเาเชื่อว่ามนุษย์มีความอยากรู้อยากเห็นมากเกินไป";
			mes "ที่จะไม่ทำการทดลอง, โดยเฉพาะอย่างยิ่งเมื่อมันเป็นสิ่งต้องห้าม.";
			mes "ข้าแค่ไม่อยากให้พี่ชายต้องมาต่อว่าข้า. เท่านั้นเอง.";
			next;
			switch(select("ใช้บริการ","ยกเลิก"))
			{
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "...อะ... อะไรนะ? ไม่!";
					mes "การมองข้าด้วยสายตาแบบนั้น";
					mes "ไม่ทำให้ข้ายอมทำมันหรอกนะ!";
					mes "ไม่, ข้าบอกว่าไม่! ไม่คือไม่! ไม่!";
					next;
					mes "[Leablem]";
					mes "ไม่, ข้าจะไม่ทำ! ไม่!";
					next;
					mes "[Leablem]";
					mes "ข้าทำไม่ได้...";
					next;
					mes "[Leablem]";
					mes "ข้าทำ... ไม่ได้...";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "อืมม...";
					next;
					mes "[Leablem]";
					mes "เจ้าเป็นคนดื้อรั้นมาก. ตกลง, ข้าจะให้ข้อยกเว้นแก่เจ้า.";
					mes "แค่อย่าบอกใครนะ,";
					mes "โดยเฉพาะพี่ชายของข้า เจ้าสัญญาได้ไหม?";
					next;
					mes "[Leablem]";
					mes "ตกลง เอาล่ะ, โปรดนำ";
					mes "^FF00002 Gold, 200,000,000 zeny -- ค่าบริการ --,";
					mes "^FF0000และ Hat of the Sun God มาให้ข้า.";
					mes "^FF0000เจ้ารู้ไหมว่าโอกาสสำเร็จคือ 90% กับบริการนี้, รู้ใช่ไหม?^000000";
					mes "เราไม่มีเวลาที่จะเสียแล้ว,";
					mes "ดังนั้นเรามาทำธุรกิจกันเลย.";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "เรามาเริ่มกันเลยดีไหม?";
						mes "อธิษฐานต่อเทพเจ้าของเจ้าเพื่อความโชคดี.";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes "ยอดเยี่ยมมาก! ว้าว, ข้าเดาว่าโอกาสสำเร็จ 90% นั้นจะเป็นเรื่องจริง!";
							mes "ดูสิ, slot ถูกเพิ่มในตำแหน่งที่สมบูรณ์แบบแล้ว.";
							mes "ยินดีด้วย.";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[Leablem]";
							mes "นี่เป็นครั้งแรกและครั้งสุดท้ายที่ข้าจะทำสิ่งนี้ให้กับเจ้า.";
							mes "อย่าบอกใครเด็ดขาด";
							mes "เกี่ยวกับเรื่องนี้. ตกลงไหม?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes "ว้าาา! โอ้ววว พระเจ้า!, พวกเราล้มเหลว!";
							mes "พระเจ้า, ข้าเดาว่าคงเป็นความโชคร้ายที่สุด...";
							mes "เราควรทำอย่างไรดี? ...นี่คือสาเหตุ";
							mes "ที่ข้าไม่อยากทำตั้งแต่แรก!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[Leablem]";
							mes "ไม่เอา, อย่าขอให้ข้าทำสิ่งที่เสี่ยงแบบนั้นอีกเลย!";
							mes "Bah~";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "อืมม...";
						next;
						mes "[Leablem]";
						mes "เจ้ามีวัตถุดิบไม่เพียงพอ.";
						mes "เพื่อน, ทำไมเจ้าถึงขอให้ข้าลองด้วยล่ะ?";
						mes "ข้าจะไม่ทำสิ่งนี้อีกต่อไปเพื่อเจ้า,";
						mes "ถึงแม้ว่าเจ้าจะเตรียมวัตถุดิบมาเพียงพอแล้วก็ตาม. Bah~";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes "อย่าบอกใครเด็ดขาด";
					mes "เกี่ยวกับสิ่งที่ข้าบอกเจ้า. ตกลงไหม?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes "เจ้าต้องการทราบข้อมูลเพิ่มเติมหรือไม่?";
			mes "เอาล่ะ, ข้าไม่คิดว่าจะมีอะไร";
			mes "ที่เจ้าจำเป็นต้องรู้เป็นพิเศษ.";
			mes "แต่อย่างน้อยข้าก็สามารถให้คำแนะนำแก่เจ้าได้.";
			next;
			mes "[Leablem]";
			mes "ช่างเทคนิค slot ในแต่ละเมือง";
			mes "ให้โอกาสความสำเร็จในการ slotting ที่แตกต่างกัน";
			mes "ขึ้นอยู่กับระดับการอัพเกรดอุปกรณ์ของเจ้า.";
			mes "ข้าไม่รู้ว่ามันเป็นเรื่องจริงหรือเปล่า,";
			mes "แต่ข้าเห็นความแตกต่างในแต่ละเมือง.";
			next;
			mes "[Leablem]";
			mes "ตัวอย่างเช่น, ช่างเทคนิค slot ในเมืองหนึ่ง";
			mes "ให้โอกาสสำเร็จสูงกว่า";
			mes "สำหรับอุปกรณ์ที่มีระดับการอัพเกรดสูงขึ้น,";
			mes "ในขณะที่ช่างในเมืองอื่นมีโอกาสสำเร็จสูง";
			mes "ในระดับการอัพเกรดต่ำกว่า.";
			next;
			mes "[Leablem]";
			mes "เมื่อเจ้าล้มเหลวในการเพิ่ม slot, อุปกรณ์นั้น";
			mes "จะเสียหายและหายไป.";
			mes "ข้าพยายามอย่างเต็มที่เพื่อที่จะประสบความสำเร็จ, แต่";
			mes "การเพิ่ม slotting เป็นงานที่ซับซ้อน.";
			next;
			mes "[Leablem]";
			mes "โอ้, มีสิ่งสำคัญที่เจ้าจะต้องระวัง.";
			mes "^FF0000พยายามอย่าพกอุปกรณ์ที่เหมือนกันสองชิ้น";
			mes "^FF0000มากับเจ้าเมื่อเจ้าเพิ่ม slots.";
			mes "^FF0000มิฉะนั้น, slots อาจถูกเพิ่มเข้าไปในรายการใดรายการหนึ่งที่ได้รับการสุ่มเลือก,";
			mes "^FF0000และในกรณีที่แย่ที่สุด, เจ้าอาจสูญเสียอุปกรณ์ที่มีค่ามากกว่าสำหรับเจ้า.^000000";
			next;
			mes "[Leablem]";
			mes "ข้าหวังว่าข้อมูลนี้จะเป็นประโยชน์กับเจ้า.";
			mes "ขอบคุณ.";
			close;
		case 5:
			mes "[Leablem]";
			mes "อย่างนั้น, ดีแล้ว.";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	Leablem#pay	86
lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	86
prt_in,22,60,6	duplicate(SocketEnchant2)	Leablem#prt	86

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Leablem]";
	if (.@item_req[1] != 0 && .@req_amount[1] != 0)
	{
		mes getitemname(.@item_id) +"... ตกลง, ดังนั้นเจ้าต้องนำ";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny เป็นค่าบริการ,";
		mes "^FF0000"+ .@req_amount[0] +"ea "+ getitemname(.@item_req[0]) +" และ "+ .@req_amount[1] +"ea "+ getitemname(.@item_req[1]) +".";
		mes "^FF0000และแน่นอน, เจ้าต้องมี "+ getitemname(.@item_id) +" มาให้ข้า.^000000";
	}
	else
	{
		mes getitemname(.@item_id) +"? ตกลง, ดังนั้นเจ้าต้องนำ";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req/1000 +",000") : .@zeny_req) +",000 zeny เป็นค่าบริการ และ "+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +" ตามข้อกำหนด.";
		mes "^FF0000และแน่นอน, อย่าลืมนำ "+ getitemname(.@item_id) +" มาให้ข้า.";
		mes "^FF0000เจ้าควรมีไอเท็มทั้งหมด.^000000";
	}
	next;
	mes "[Leablem]";
	mes "เจ้านำพวกมันทั้งหมดมาแล้วหรือยัง?";
	mes "เพื่อให้เจ้าทราบ, หากเจ้าล้มเหลวในการสร้าง slot,";
	mes "เจ้าจะสูญเสียวัสดุทั้งหมดที่จำเป็นรวมถึง "+ ((getiteminfo(.@item_id, ITEMINFO_LOCATIONS)&EQP_HAND_R) ? "weapon(อาวุธ)" : "armor(ชุดเกราะ)") +" ที่เป็นเป้าหมายด้วย.";
	mes "นอกจากนี้, จำไว้ว่าหาก "+ ((getiteminfo(.@item_id, ITEMINFO_LOCATIONS)&EQP_HAND_R) ? "weapon(อาวุธ)" : "armor(ชุดเกราะ)") +" ได้ถูกอัพเกรด, และถูกใส่การ์ดแล้ว,";
	mes "เจ้าจะสูญเสียมันไปแม้ว่าเจ้าจะสร้าง slot สำเร็จก็ตาม.";
	next;
	switch(select("ยืนยันการสร้าง slot:ไว้ลองใหม่ครั้งหน้า"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no .@req_amount[0] and .@req_amount[1] provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Leablem]";
				mes "เอาล่ะ, ถ้างั้นเริ่มงานกันเลย!";
				mes "เจ้าควรอธิษฐานขอให้ประสบผลสำเร็จจะดีกว่า.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[Leablem]";
					mes "เยี่ยมมาก, ดูเหมือนว่าจะประสบความสำเร็จ.";
					mes "ดูเหมือนจะทำได้อย่างดีเยี่ยม. ยินดีด้วย!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Leablem]";
					mes "แล้วพบกันใหม่, นะเพื่อน!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes "ว้าาา! ...ข้าขอโทษ, มันล้มเหลว.";
					mes "อย่างไรก็ตาม, ข้าไม่ได้ทำผิดพลาดใดๆ เลย.";
					mes "นี่คือโชคของเจ้า, และมันถูกลิขิตโดยพระเจ้า, โอเคไหม?";
					mes "อย่าผิดหวังมากนัก,";
					mes "และลองครั้งต่อไป.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Leablem]";
					mes "แล้วพบกันใหม่, นะเพื่อน!";
					close;
				}
			}
			else
			{
				mes "[Leablem]";
				mes "เจ้าโง่หรือเปล่า? เจ้าไม่ได้นำสิ่งของที่จำเป็นมาทั้งหมด!";
				mes "ไปนำพวกมันมาเร็วเข้า!";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes "ไว้เจอกันใหม่คราวหน้านะ.";
			close;
	}
}
