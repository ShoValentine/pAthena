//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= Hero Ring enchants NPCs - Walkthrough Conversion.
//===== Changelogs: ==========================================
//= 1.0 First Version. [Capuche]
//============================================================

moro_cav,32,73,5	script	Hero Token Exchanger#1	4_F_SHADOWCHASER,{
	disable_items;
	if (checkweight(1201,1) == 0) {// custom
		mes "The kind of items you have is too various. Retry after reducing the sort of items.";
		close;
	}
	if ((MaxWeight - Weight) < 10000) {
		mes "You are overburdened, so you cannot proceed it anymore. Please try again after reduce the weight.";
		close;
	}
	mes "[Hero Token Exchanger]";
	mes "If you fought Morroc and collected";
	mes "<ITEM>"+getitemname(6684)+"<INFO>6684</INFO><ITEM>,";
	mes "I can exchange them with a special gear for you.";
	mes "What do you want exchange Hero Tokens with?";
	while(true) {
		next;
		switch( select(
			"Exchange 3 x tokens for the Prize of Hero.",
			"Exchange 5 x tokens for the High Weapon Box.",
			"Exchange 30 x tokens for the Hero Ring.",
			"Exchange 15 x tokens for Random Hero Shoe.",
			"Exchange 25 x tokens for specific Hero Shoe.",
			"Exchange 35 x tokens for Random Hero Armor.",
			"Exchange 50 x tokens for specific Hero Armor.",
			"End conversation." ) ) {
		case 1:
			mes "[Hero Token Exchanger]";
			mes "I will exchange 3 x Hero Token for a <ITEM>"+getitemname(22537)+"<INFO>22537</INFO></ITEM>.";
			mes "Do you want to exchange?";
			if (select( "Previous menu.", "Exchange 3 x Hero Tokens." ) == 1) {
				mes "[Hero Token Exchanger]";
				mes "Think again.";
				continue;
			}
			if (countitem(6684) < 3) {
				mes "[Hero Token Exchanger]";
				mes "<ITEM>"+getitemname(6684)+"<INFO>6684</INFO><ITEM>,";
				mes "is given to those who bravely fought Morroc.";
				mes "You don't have 3 x ^006400Hero Tokens^000000,";
				mes "and therefore I can't assist you.";
				continue;
			}
			mes "[Hero Token Exchanger]";
			mes "I'll exchange";
			mes "3 x ^006400Hero Tokens^000000";
			mes "with 1 x ^006400"+getitemname(22537)+"^000000.";
			mes "Anything else?";
			delitem 6684,3;	// Hero Token
			getitem 22537,1;	// Prize Of Hero
			continue;


		case 2:
			mes "[Hero Token Exchanger]";
			mes "I will exchange 5 x Hero Token for a <ITEM>"+getitemname(12623)+"<INFO>12623</INFO></ITEM>.";
			mes "Do you want to exchange?";
			if (select( "Previous menu.", "Exchange 5 x Hero Tokens." ) == 1) {
				mes "[Hero Token Exchanger]";
				mes "Think again.";
				continue;
			}
			if (countitem(6684) < 5) {
				mes "[Hero Token Exchanger]";
				mes "<ITEM>"+getitemname(6684)+"<INFO>6684</INFO><ITEM>,";
				mes "is given to those who bravely fought Morroc.";
				mes "You don't have 5 x ^006400Hero Tokens^000000,";
				mes "and therefore I can't assist you.";
				continue;
			}
			mes "[Hero Token Exchanger]";
			mes "I'll exchange";
			mes "5 x ^006400Hero Tokens^000000";
			mes "with 1 x ^006400"+getitemname(12623)+"^000000.";
			mes "Anything else?";
			delitem 6684,5;	// Hero Token
			getitem 12623,1;	// Prize Of Hero
			continue;

		case 3:
			mes "[Hero Token Exchanger]";
			mes "This Hero Ring may not look impressive,";
			next;
			mes "[Hero Token Exchanger]";
			mes "but take it to";
			mes "Expert Enchanter Byrnes,";
			mes "and he can Enchant";
			mes "all 4 sockets in it";
			mes "and bring out";
			mes "its latent abilities.";
			next;
			mes "[Hero Token Exchanger]";
			mes "Hero Ring";
			mes "can aid you";
			mes "in subjugating";
			mes "Morroc.";
			mes "Any more questions?";
			next;
			mes "- Hero Ring -";
			mes "^006400A ring made for warriors^000000";
			mes "^006400on the quest to defeat Morroc..^000000";
			mes "^006400When Enchanted,^000000";
			mes "^006400it lends its owner^000000";
			mes "^006400the power to confront Morroc.^000000";
			mes "^006400Clas: Accessory/DEF: 0^000000";
			mes "^006400Weight: 20/Required Level: 160^000000";
			mes "^006400Slot: 0/Equip To: All Jobs^000000";
			next;
			mes "[Hero Token Exchanger]";
			mes "Hero Ring requires";
			mes "30 x Hero Tokens";
			mes "for the exchange.";
			mes "Do you want to exchange?";
			next;
			if (select( "Previous menu.", "Exchange 30 x Hero Tokens." ) == 1) {
				mes "[Hero Token Exchanger]";
				mes "Think";
				mes "again.";
				continue;
			}
			if (countitem(6684) < 30) {
				mes "[Hero Token Exchanger]";
				mes "^006400Hero Token^000000";
				mes "is given to";
				mes "those who bravely fought Morroc.";
				mes "You don't have";
				mes "30 x ^006400Hero Tokens^000000,";
				mes "and therefore I can't assist you.";
				continue;
			}
			mes "[Hero Token Exchanger]";
			mes "I'll exchange";
			mes "30 x ^006400Hero Tokens^000000";
			mes "with 1 x ^006400Hero Ring^000000.";
			mes "Anything else?";
			delitem 6684,30;	// TokenOfHero
			getitem 2981,1;		// RingOfHero
			continue;

		case 4:
			mes "[Hero Token Exchanger]";
			mes "I will exchange 15 x Hero Token for a random hero shoe as listed below:";
			mes "- <ITEM>"+getitemname(22035)+"<INFO>22035</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(22036)+"<INFO>22036</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(22037)+"<INFO>22037</INFO></ITEM>";
			mes "Do you want to exchange?";
			if (select( "Previous menu.", "Exchange 15 x Hero Tokens." ) == 1) {
				mes "[Hero Token Exchanger]";
				mes "Think again.";
				continue;
			}
			if (countitem(6684) < 15) {
				mes "[Hero Token Exchanger]";
				mes "<ITEM>"+getitemname(6684)+"<INFO>6684</INFO><ITEM>,";
				mes "is given to those who bravely fought Morroc.";
				mes "You don't have 15 x ^006400Hero Tokens^000000,";
				mes "and therefore I can't assist you.";
				continue;
			}
			mes "[Hero Token Exchanger]";
			mes "I'll exchange";
			mes "15 x ^006400Hero Tokens^000000";
			mes "with 1 x ^006400random hero shoe^000000.";
			mes "Anything else?";
			delitem 6684,15;	// Hero Token
			setarray .@shoe[0],22035,22036,22037;
			getitem .@shoe[rand(2)],1;	// random shoe
			continue;

		case 5:
			mes "[Hero Token Exchanger]";
			mes "I will exchange 25 x Hero Token for any hero shoe as listed below:";
			mes "- <ITEM>"+getitemname(22035)+"<INFO>22035</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(22036)+"<INFO>22036</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(22037)+"<INFO>22037</INFO></ITEM>";
			mes "Which one do you want to exchange?";
			.@s = select( "Previous menu.", "Exchange for "+getitemname(22035), "Exchange for "+getitemname(22036), "Exchange for "+getitemname(22037) );
			if(.@s == 1) {
				mes "[Hero Token Exchanger]";
				mes "Think again.";
				continue;
			}
			if (countitem(6684) < 25) {
				mes "[Hero Token Exchanger]";
				mes "<ITEM>"+getitemname(6684)+"<INFO>6684</INFO><ITEM>,";
				mes "is given to those who bravely fought Morroc.";
				mes "You don't have 25 x ^006400Hero Tokens^000000,";
				mes "and therefore I can't assist you.";
				continue;
			}
			mes "[Hero Token Exchanger]";
			mes "I'll exchange";
			mes "25 x ^006400Hero Tokens^000000";
			switch(.@s)
			{
				case 2:
					mes "with 1 x ^006400"+getitemname(22035)+"^000000.";
					mes "Anything else?";
					delitem 6684,25;	// Hero Token
					getitem 22035,1;	// Hero Nependess Shoes [1]
					continue;
				case 3:
					mes "with 1 x ^006400"+getitemname(22036)+"^000000.";
					mes "Anything else?";
					delitem 6684,25;	// Hero Token
					getitem 22036,1;	// Hero Silverleather Boots [1]
					continue;
				case 4:
					mes "with 1 x ^006400"+getitemname(22037)+"^000000.";
					mes "Anything else?";
					delitem 6684,25;	// Hero Token
					getitem 22037,1;	// Hero Ungoliant Boots [1]
					continue;
			}
			continue;

		case 6:
			mes "[Hero Token Exchanger]";
			mes "I will exchange 35 x Hero Token for a random hero shoe as listed below:";
			mes "- <ITEM>"+getitemname(15093)+"<INFO>15093</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15094)+"<INFO>15094</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15095)+"<INFO>15095</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15096)+"<INFO>15096</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15097)+"<INFO>15097</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15098)+"<INFO>15098</INFO></ITEM>";
			mes "Do you want to exchange?";
			if (select( "Previous menu.", "Exchange 35 x Hero Tokens." ) == 1) {
				mes "[Hero Token Exchanger]";
				mes "Think again.";
				continue;
			}
			if (countitem(6684) < 35) {
				mes "[Hero Token Exchanger]";
				mes "<ITEM>"+getitemname(6684)+"<INFO>6684</INFO><ITEM>,";
				mes "is given to those who bravely fought Morroc.";
				mes "You don't have 35 x ^006400Hero Tokens^000000,";
				mes "and therefore I can't assist you.";
				continue;
			}
			mes "[Hero Token Exchanger]";
			mes "I'll exchange";
			mes "35 x ^006400Hero Tokens^000000";
			mes "with 1 x ^006400random hero armor^000000.";
			mes "Anything else?";
			delitem 6684,35;	// Hero Token
			setarray .@armor[0],15093,15094,15095,15096,15097,15098;
			getitem .@armor[rand(5)],1;	// random armor
			continue;

		case 7:
			mes "[Hero Token Exchanger]";
			mes "I will exchange 50 x Hero Token for any hero armor as listed below:";
			mes "- <ITEM>"+getitemname(15093)+"<INFO>15093</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15094)+"<INFO>15094</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15095)+"<INFO>15095</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15096)+"<INFO>15096</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15097)+"<INFO>15097</INFO></ITEM>";
			mes "- <ITEM>"+getitemname(15098)+"<INFO>15098</INFO></ITEM>";
			mes "Which one do you want to exchange?";
			.@s = select( "Previous menu.",
				"Exchange for "+getitemname(15093),
				"Exchange for "+getitemname(15094),
				"Exchange for "+getitemname(15095),
				"Exchange for "+getitemname(15096),
				"Exchange for "+getitemname(15097),
				"Exchange for "+getitemname(15098) );
			if(.@s == 1) {
				mes "[Hero Token Exchanger]";
				mes "Think again.";
				continue;
			}
			if (countitem(6684) < 50) {
				mes "[Hero Token Exchanger]";
				mes "<ITEM>"+getitemname(6684)+"<INFO>6684</INFO><ITEM>,";
				mes "is given to those who bravely fought Morroc.";
				mes "You don't have 50 x ^006400Hero Tokens^000000,";
				mes "and therefore I can't assist you.";
				continue;
			}
			mes "[Hero Token Exchanger]";
			mes "I'll exchange";
			mes "50 x ^006400Hero Tokens^000000";
			switch(.@s)
			{
				case 2:
					mes "with 1 x ^006400"+getitemname(15093)+"^000000.";
					mes "Anything else?";
					delitem 6684,50;	// Hero Token
					getitem 15093,1;	// Hero Plate [1]
					continue;
				case 3:
					mes "with 1 x ^006400"+getitemname(15094)+"^000000.";
					mes "Anything else?";
					delitem 6684,50;	// Hero Token
					getitem 15094,1;	// Hero Magic Coat [1]
					continue;
				case 4:
					mes "with 1 x ^006400"+getitemname(15095)+"^000000.";
					mes "Anything else?";
					delitem 6684,50;	// Hero Token
					getitem 15095,1;	// Hero Judgement Shawl [1]
					continue;
				case 5:
					mes "with 1 x ^006400"+getitemname(15096)+"^000000.";
					mes "Anything else?";
					delitem 6684,50;	// Hero Token
					getitem 15096,1;	// Hero Trade Mail [1]
					continue;
				case 6:
					mes "with 1 x ^006400"+getitemname(15097)+"^000000.";
					mes "Anything else?";
					delitem 6684,50;	// Hero Token
					getitem 15097,1;	// Hero Hidden Cloth [1]
					continue;
				case 7:
					mes "with 1 x ^006400"+getitemname(15098)+"^000000.";
					mes "Anything else?";
					delitem 6684,50;	// Hero Token
					getitem 15098,1;	// Hero Target Suit [1]
					continue;
			}
			continue;

		case 8:
			mes "[Hero Token Exchanger]";
			mes "One day";
			mes "you'll find yourself";
			mes "in need of heroic gears.";
			close;
		}
	}
}

moro_cav,34,65,5	script	Expert Enchanter Byrnes	1_M_03,{
	disable_items;
	mes "[Byrnes]";
	mes "Have you heard of Hero Ring? Only true heroes deserve to possess that magnificent ring, and I've been waiting for one.";
	mes "What can I do for you?";
	next;
	switch( select( "Enchant Hero Ring.", "Reset Enchantments on Hero Ring.", "Cancel." ) ) {
	case 1:
		mes "[Byrnes]";
		mes "I can Enchant only equipped rings. Which one of the equipped rings do you want to Enchant?";
		next;
		setarray .@eqi_slot[0], EQI_ACC_L, EQI_ACC_R;
		.@s = select( "Left", "Right" ) - 1;
		switch( getequipid(.@eqi_slot[.@s]) ) {
		case -1:
			mes "[Byrnes]";
			mes "Please equip the Hero Ring that you want to enchant.";
			close;
		case 2981:
			break;
		default:
			mes "[Byrnes]";
			mes "Please equip your Hero Ring. I can't Enchant anything else.";
			close;
		}
		// custom at this point
		setarray .@card[0],
			getequipcardid(.@eqi_slot[.@s],0),
			getequipcardid(.@eqi_slot[.@s],1),
			getequipcardid(.@eqi_slot[.@s],2),
			getequipcardid(.@eqi_slot[.@s],3);

		if (.@card[0] > 0) {
			mes "[Byrnes]";
			mes "A fully enchanted Hero Ring cannot be enchanted any further.";
			close;
		}
		if (.@card[3] == 0) {
			setarray .@enchant_list$[0], "DEF", "MDEF";
			.@slot = 4;
		}
		else if (.@card[2] == 0) {
			setarray .@enchant_list$[0], "Max HP", "Max SP";
			.@slot = 3;
		}
		else if (.@card[1] == 0) {
			setarray .@enchant_list$[0], "Atk", "MAtk";
			.@slot = 2;
		}
		else {
			setarray .@enchant_list$[0], "STR", "INT", "AGI", "VIT", "DEX", "LUK";
			.@slot = 1;
		}
		mes "[Byrnes]";
		mes "Which ability do you want as an enchantment on slot " + .@slot + "?";
		next;
		.@enchant_list_size = getarraysize(.@enchant_list$);
		for ( .@i = 0; .@i < .@enchant_list_size; ++.@i )
			.@menu$ += .@enchant_list$[.@i] + ":";
		.@type = select(.@menu$) - 1;

		switch(.@slot) {
		case 4:
			switch(.@type) {// enchant list index
			case 0:	// DEF
				setarray .@bonus[0], 4791, 4792, 4793, 4794, 4893;
				break;
			default:	// MDEF
				setarray .@bonus[0], 4890, 4786, 4891, 4787, 4892;
				break;
			}
			break;
		case 3:
			switch(.@type) {
			case 0:	// Max HP
				setarray .@bonus[0], 4861, 4862, 4867, 4868, 4900;
				break;
			default:	// Max SP
				setarray .@bonus[0], 4800, 4870, 4871, 4801, 4802;
				break;
			}
			break;
		case 2:
			switch(.@type) {
			case 0:	// Atk
				setarray .@bonus[0], 4882, 4766, 4767, 4894, 4895;
				break;
			default:	// Matk
				setarray .@bonus[0], 4883, 4896, 4897, 4898, 4899;
				break;
			}
			break;
		case 1:
			switch(.@type) {
			case 0:	// STR
				setarray .@bonus[0], 4700, 4701, 4702, 4703, 4704;
				break;
			case 1:	// INT
				setarray .@bonus[0], 4710, 4711, 4712, 4713, 4714;
				break;
			case 2:	// AGI
				setarray .@bonus[0], 4730, 4731, 4732, 4733, 4734;
				break;
			case 3:	// VIT
				setarray .@bonus[0], 4740, 4741, 4742, 4743, 4744;
				break;
			case 4:	// DEX
				setarray .@bonus[0], 4720, 4721, 4722, 4723, 4724;
				break;
			default:	// LUK
				setarray .@bonus[0], 4750, 4751, 4752, 4753, 4754;
				break;
			}
			break;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@eqi_slot[.@s], 2981) || callfunc("F_IsEquipCardHack", .@eqi_slot[.@s], .@card[0], .@card[1], .@card[2], .@card[3]))
			close;

		.@bonus_size = getarraysize(.@bonus);
		.@card[.@slot-1] = .@bonus[ rand(.@bonus_size) ];	// unknown rates
		specialeffect2 EF_ANGEL2;
		mes "[Byrnes]";
		mes "Enchantment completed!";
		delequip .@eqi_slot[.@s];
		getitem2 2981,1,1,0,0,.@card[0],.@card[1],.@card[2],.@card[3];
		close;
	case 2:
		mes "[Byrnes]";
		mes "To reset Enchantments on your Hero Ring, I need 3 Hero Tokens.";
		next;
		if (countitem(6684) < 3) {
			mes "[Byrnes]";
			mes "You don't have enough Hero Tokens. I can't reset your Hero Ring.";
			close;
		}
		mes "[Byrnes]";
		mes "I can reset only equipped rings. Which one of the equipped rings do you want to reset?";
		next;
		setarray .@eqi_slot[0], EQI_ACC_L, EQI_ACC_R;
		.@s = select( "Left", "Right" ) - 1;
		switch( getequipid(.@eqi_slot[.@s]) ) {
		case -1:
			mes "[Byrnes]";
			mes "You first have to equip the item to reset its Enchantment status.";
			close;
		case 2981:
			break;
		default:
			mes "[Byrnes]";
			mes "Please equip your Hero Ring. I can't reset anything else.";
			close;
		}
		// custom at this point
		if (countitem(6684) < 3) {
			mes "[Byrnes]";
			mes "You don't have enough Hero Tokens. I can't reset your Hero Ring.";
			close;
		}
		setarray .@card[0],
			getequipcardid(.@eqi_slot[.@s],0),
			getequipcardid(.@eqi_slot[.@s],1),
			getequipcardid(.@eqi_slot[.@s],2),
			getequipcardid(.@eqi_slot[.@s],3);

		if (.@card[3] == 0) {
			mes "[Byrnes]";
			mes "Your Hero Ring is currently in a state without enchantment.";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@eqi_slot[.@s], 2981) || callfunc("F_IsEquipCardHack", .@eqi_slot[.@s], .@card[0], .@card[1], .@card[2], .@card[3]))
			close;

		specialeffect2 EF_ANGEL2;
		mes "[Byrnes]";
		mes "Initialization completed.";
		delitem 6684,3;
		delequip .@eqi_slot[.@s];
		getitem 2981,1;
		close;
	case 3:
		close;
	}
}
