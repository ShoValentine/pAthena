//===== rAthena Script =======================================
//= NPCs related to Glastheim (Nightmare)
//===== By: ==================================================
//= Ridley8819
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Additional Comments: =================================
//= 1.0 Initial release [Ridley8819]
//============================================================

glast_01,195,273,4	script	Hugin's Follower#n_gh	123,{
	mes "[Hugin's Follower]";
	mes "I can't move between time and dimension like a master Hugin but I can pretend to move.";
	next;
	mes "[Hugin's Follower]";
	mes "What about this Glast Heim is born with powerful dark force?";
	next;
	if( select( "I don't care:That is interesting, please tell me more" ) == 2 ){
		mes "[Hugin's Follower]";
		mes "As learning ability from Hugin, I just find out the path to Old Glast Heim of more powerful dark force.";
		next;
		mes "[Hugin's Follower]";
		mes "How is it? Do you want to explore there?";
		next;
		if( select( "No, I want to stop it:I really want to do that!" ) == 2 ){
			mes "[Hugin's Follower]";
			mes "Sure that is not free~ I have to get something little bit. Hoho...";
			next;
			mes "[Hugin's Follower]";
			mes "I can send you just two place. How is it? Do you want to try?";
			next;
			switch( select( "Cancel:Monastery Cemetery (Lv100~130) - 5000z:Corridor of 2nd floor (Over Lv140) - 10000z" ) ){
				case 1:
					mes "[Hugin's Follower]";
					mes "Alright, please come back whenever you change mind. I think you will have lots of thing to talk with me...";
					close;
				case 2:
					.@map$ = "gl_chyard_";
					.@x = 148;
					.@y = 144;
					.@zeny = 5000;
					break;
				case 3:
					.@map$ = "gl_cas02_";
					.@x = 104;
					.@y = 31;
					.@zeny = 10000;
					break;
			}
			if( Zeny < .@zeny ){
				mes "[Hugin's Follower]";
				mes "What, did you ask me without money?";
				close;
			}
			Zeny -= .@zeny;
			warp .@map$, .@x, .@y;
			end;
		}
	}
	mes "[Hugin's Follower]";
	mes "Well~ If you are not interested in it, never mind.";
	close;
}

gl_chyard_,148,149,4	script	Hugin's Follower#n_gh1	123,{
	mes "[Hugin's Follower]";
	mes "You want to break?";
	mes "Ok, let's get out of here.?";
	next;
	if( select( "Ah, wrong click... I mean I was not talk to you:Please let me out from here" ) == 1 ){
		mes "[Hugin's Follower]";
		mes "Alright just tell me whenever you want to leave.";
		close;
	}
	warp "glast_01", 210, 269;
	end;
}

gl_cas02_,104,36,4	duplicate(Hugin's Follower#n_gh1)	Hugin's Follower#n_gh2	123
