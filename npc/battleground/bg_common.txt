//===== rAthena Script ======================================= 
//= BattleGround System - Common NPCs
//===== By: ================================================== 
//= ????, L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Battleground NPCs:
//= - Generals and Aides
//= - Battleground Warper
//= - Kafra and Repairman.
//= - GM Management NPC
//= - Badge Exchanger (Tierra and Flavius)
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Updated several NPCs to Official.
//= 1.2 Updated Repairman NPC
//= 1.3 Optimized "Erundek" NPC. [Euphy]
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added VIP features and created a reward function. [Euphy]
//============================================================

// Generals
//============================================================
bat_room,161,158,3	script	Gen. Guillaume's Aide#01	419,{ end; }
bat_room,161,160,3	script	Gen. Guillaume's Aide#03	419,{ end; }

bat_room,160,141,3	script	Prince Croix	416,{
	cutin "bat_crua1",2;
	mes "[Prince Croix]";
	mes "Petualang yang bijaksana, mengapa kamu tidak meminjamkan kami kekuatanmu untuk kemenangan?";
	next;
	switch(select("Apa alasan Pertempuran?:Ceritakan tentang Jenderal Guillaume")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "Raja agung Maroll, Marcel Marrollo VII, sedang sakit keras akhir-akhir ini.";
		mes "Yang Mulia telah menyatakan bahwa dia akan menyerahkan masa depan Maroll kepada saya atau pangeran ke-3, Jenderal Guillaume.";
		next;
		mes "[Prince Croix]";
		mes "Jenderal Guillaume mungkin memiliki keuntungan dalam pertempuran ini karena dia adalah jenderal besar Maroll, tetapi itu tidak otomatis berarti dia akan menang.";
		mes "Saya ingin memenangkan pertempuran ini sehingga saya dapat membawa kemakmuran bagi orang-orang Maroll. Mereka sudah cukup menderita akibat perang...";
		next;
		switch(select("Ya, saya ingin bergabung dengan Anda.:Akhiri Percakapan")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "Terima kasih banyak. Saya merasa bisa menang dengan bantuan petualang seperti Anda.";
			mes "Sekarang, silakan turun dan bergabunglah dengan rekan-rekan Anda untuk mengasah keterampilan mereka untuk melawan musuh!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "Untuk Maroll!";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Prince Croix]";
		mes "Pangeran ke-3 Guillaume adalah jenderal besar Maroll.";
		mes "Buang-buang waktu untuk menjelaskan kepada Anda betapa hebatnya dia sebagai pemimpin atau panglima perang, karena dia memimpin kekuatan militer besar Maroll.";
		next;
		mes "[Prince Croix]";
		mes "Sayangnya, ada sesuatu yang dia dan pengikutnya tidak sadari:";
		mes "Apakah orang-orang Maroll benar-benar ingin mereka menghabiskan begitu banyak uang untuk kekuatan militer?";
		mes "Kami sudah cukup menderita akibat perang.";
		mes "Saya percaya senjata bukanlah cara terbaik untuk membawa kemakmuran bagi suatu bangsa.";
		next;
		mes "[Prince Croix]";
		mes "Saya tidak ingin menumpahkan darah, tetapi saya tidak punya pilihan selain berjuang untuk kemungkinan perdamaian dan demi rakyat saya.";
		next;
		switch(select("Ya, saya ingin bergabung dengan Anda.:Akhiri Percakapan")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Prince Croix]";
			mes "Terima kasih banyak. Saya merasa bisa menang dengan bantuan petualang seperti Anda.";
			mes "Sekarang, silakan turun dan bergabunglah dengan rekan-rekan Anda untuk mengasah keterampilan mereka untuk melawan musuh!";
			break;
		case 2:
			mes "[Prince Croix]";
			mes "Untuk Maroll!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Prince Croix's Aide#01	415,{ end; }
bat_room,161,142,3	script	Prince Croix's Aide#02	415,{ end; }

bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "Petualang berdarah panas, kami membutuhkan kemampuanmu untuk memenangkan pertempuran ini.";
	next;
	switch(select("Apa alasan Pertempuran?:Ceritakan tentang Pangeran Croix")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "Raja agung kita, Marcel Marrollo VII, sedang sakit keras akhir-akhir ini.";
		mes "Yang Mulia telah menyatakan bahwa dia telah memilih saya atau Pangeran Croix sebagai raja berikutnya di antara 9 putranya.";
		next;
		mes "[General Guillaume]";
		mes "Dua raja tidak bisa berbagi bangsa! Hanya satu pemenang dari pertempuran yang ditunjuk Yang Mulia yang akan dinobatkan.";
		next;
		mes "[General Guillaume]";
		mes "Namun, ini bukan hanya pertempuran di antara kami. Pertempuran ini akan menentukan masa depan negara ini.";
		mes "Saya berjanji pada kehormatan saya untuk membuktikan bahwa sayalah yang dapat melindungi Marol ini dari ancaman luar.";
		next;
		switch(select("Ya, saya ingin bergabung dengan Anda.:Akhiri Percakapan")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Selamat datang di pasukanku, kawan.";
			mes "Matamu memberitahuku bahwa kau adalah seorang prajurit yang bisa kupercaya.";
			next;
			mes "[General Guillaume]";
			mes "Sekarang, naik ke atas dan ajukan pertempuran dengan rekan-rekanmu.";
			mes "Saya yakin mereka akan menyambut Anda dengan sepenuh hati!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Saya akan menjadi orang yang akan menangkap bendera!";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "Pangeran Croix ke-5 saat ini bergelar Perdana Menteri Maroll.";
		mes "Dia pikir semua masalah nasional suatu bangsa dapat dibahas dan ditentukan di atas meja,";
		mes "dan percaya akan hidup berdampingan secara damai dengan negara-negara lain.";
		next;
		mes "[General Guillaume]";
		mes "Dia terlalu bodoh untuk mengakui bahwa apa yang disebut perdamaian dibangun di atas kehidupan yang tak terhitung jumlahnya yang dikorbankan dalam perang sementara warga biasa dan kelas atas dapat hidup, tidak menyadari kengerian yang memungkinkan mereka hidup seperti itu.";
		next;
		mes "[General Guillaume]";
		mes "Dia terlalu naif untuk memahami kenyataan....";
		mes "Saya tidak bisa meninggalkan Marol untuk orang seperti dia yang hidup dalam mimpi!";
		next;
		mes "[General Guillaume]";
		mes "Keyakinannya yang tidak realistis akan menenggelamkan negara ini dalam kemiskinan dan membuat rakyatnya lemah. Jika dia menjadi raja, Marroll tidak akan pernah berhenti dari gempuran negara lain.";
		mes "Saya ingin mengajarinya apa yang membuat negara kecil ini begitu kuat dan makmur. Ini adalah kekuatan militer!";
		next;
		switch(select("Saya ingin bergabung dengan tentara Anda!:Akhiri Percakapan")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Selamat datang di pasukanku, kawan.";
			mes "Matamu memberitahuku bahwa kau adalah seorang prajurit yang bisa kupercaya.";
			next;
			mes "[General Guillaume]";
			mes "Sekarang, naik ke atas dan ajukan pertempuran dari rekan-rekanmu.";
			mes "Saya yakin mereka akan menyambut Anda dengan sepenuh hati!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Saya akan menjadi orang yang akan menangkap bendera!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}

// Flags
//============================================================
//bat_room,140,160,3	script	Guillaume Base#roomflag1	973,{ end; }
//bat_room,167,160,3	script	Guillaume Base#roomflag2	973,{ end; }
//bat_room,140,139,3	script	Croix Base#roomflag1	974,{ end; }
//bat_room,167,139,3	script	Croix Base#roomflag2	974,{ end; }

// BattleGround Warper
//============================================================
bat_room,148,150,5	script	Teleporter#Battlefield	124,{
	mes "[Teleporter]";
	mes "Apakah Anda ingin meninggalkan medan perang? Gunakan layanan saya untuk kembali ke kota.";
	next;
	switch(select("Meninggalkan:Jangan Tinggalkan")) {
	case 1:
		mes "[Teleporter]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@mapname$[0],"Prontera.","prontera";
			setarray .@xy[0],116,72;
			break;
		case 2:
			setarray .@mapname$[0],"Morocc Ruins.","moc_ruins";
			setarray .@xy[0],152,48;
			break;
		case 3:
			setarray .@mapname$[0],"Al De Baran.","aldebaran";
			setarray .@xy[0],168,112;
			break;
		case 4:
			setarray .@mapname$[0],"Geffen.","geffen";
			setarray .@xy[0],120,39;
			break;
		case 5:
			setarray .@mapname$[0],"Payon.","payon";
			setarray .@xy[0],161,58;
			break;
		case 6:
			setarray .@mapname$[0],"Lighthalzen.","lighthalzen";
			setarray .@xy[0],159,93;
			break;
		case 7:
			setarray .@mapname$[0],"Rachel.","rachel";
			setarray .@xy[0],115,124;
			break;
		case 8:
			setarray .@mapname$[0],"Morocc.","morocc";
			setarray .@xy[0],156,46;
			break;
		}
		mes "Anda akan dikirim kembali ke "+.@mapname$[0]+".";
		close2;
		warp .@mapname$[1],.@xy[0],.@xy[1];
		break;
	case 2:
		mes "[Teleporter]";
		mes "Saya akan berada di sini kapan pun Anda membutuhkan layanan saya.";
		close;
	}
	end;
}

-	script	Maroll Battle Recruiter::BatRecruit	728,{
	mes "[Maroll Battle Recruiter]";
	mes "Selamat siang, petualang.";
	mes "Saya seorang ksatria dari negara jauh bernama Kerajaan Maroll.";
	next;
	mes "[Maroll Battle Recruiter]";
	mes "Dua pangeran kerajaan sekarang berjuang untuk tahta Marroll, dan membutuhkan tentara berpengalaman seperti Anda.";
	mes "Bagaimana Anda ingin meminjamkan kekuatan Anda kepada salah satu pangeran di Kerajaan Maroll?";
	next;
	switch(select("Bergabung:Jangan Bergabung")) {
	case 1:
		mes "[Maroll Battle Recruiter]";
		mes "Semoga dewa perang memberkati Anda.";
		close2;
		getmapxy(.@mapname$,.@x,.@y,BL_NPC);
		if (.@mapname$ == "prontera")
			set bat_return,1;
		else if (.@mapname$ == "moc_ruins")
			set bat_return,2;
		else if (.@mapname$ == "aldebaran")
			set bat_return,3;
		else if (.@mapname$ == "geffen")
			set bat_return,4;
		else if (.@mapname$ == "payon")
			set bat_return,5;
		else if (.@mapname$ == "lighthalzen")
			set bat_return,6;
		else if (.@mapname$ == "rachel")
			set bat_return,7;
		else if (.@mapname$ == "morocc")
			set bat_return,8;
		else
			set bat_return,1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[Maroll Battle Recruiter]";
		mes "Saya akan selalu ditempatkan di sini untuk lebih banyak tentara. Jangan ragu untuk kembali kapan pun Anda tertarik.";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit1	728
moc_ruins,75,162,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit2	728
aldebaran,146,109,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit3	728
geffen,109,66,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit4	728
payon,189,105,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit5	728
lighthalzen,153,86,5	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit6	728
rachel,149,138,3	duplicate(BatRecruit)	Maroll Battle Recruiter::BatRecruit7	728

// Additional warps
// Empty
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Empty
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Free BG
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Free BG
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Free BG
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Free BG
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Free BG
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Empty
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Empty
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Kafra Staff::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Repairman
//============================================================
bat_room,138,144,4	script	Repairman#bg	86,{
	callfunc "repairmain","Repairman";
	end;
}

// GM Management NPC
//============================================================
bat_room,1,151,3	script	Switch#batgnd	81,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Perintah telah dibatalkan.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Dapatkah saya membantu Anda?";
		next;
		switch(select("Tutup Medan Perang:Buka Medan Perang:Mengatur ulang a01:Mengatur ulang b01:Mengatur ulang a02:Mengatur ulang b02")) {
		case 1:
			disablenpc "Tierra Gorge Officer#01a";
			disablenpc "Tierra Gorge Officer#02a";
			disablenpc "Tierra Gorge Officer#01b";
			disablenpc "Tierra Gorge Officer#02b";
			disablenpc "Flavius Officer#01a";
			disablenpc "Flavius Officer#01b";
			disablenpc "Flavius Officer#02a";
			disablenpc "Flavius Officer#02b";
			break;
		case 2:
			enablenpc "Tierra Gorge Officer#01a";
			enablenpc "Tierra Gorge Officer#02a";
			enablenpc "Tierra Gorge Officer#01b";
			enablenpc "Tierra Gorge Officer#02b";
			enablenpc "Flavius Officer#01a";
			enablenpc "Flavius Officer#01b";
			enablenpc "Flavius Officer#02a";
			enablenpc "Flavius Officer#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "Menyelesaikan";
		close;
	}
}

// Badges Exchange
//============================================================
bat_room,160,150,3	script	Erundek	109,{
	if (checkweight(1201,1) == 0) {
		mes "- Tunggu sebentar !! -";
		mes "- Saat ini Anda sedang membawa -";
		mes "- erlalu banyak barang bersamamu. -";
		mes "- Silakan coba lagi -";
		mes "- setelah Anda kehilangan berat badan. -";
		close;
	}
	mes "[Erundek]";
	mes "Apakah kamu memiliki lencana medan perang?";
	mes "Saya bisa menukarkan Bravery Badge dan Valor Badge dengan barang hadiah.";
	next;
	switch(select("Lencana Pertukaran:Periksa Katalog")) {
	case 1:
		mes "[Erundek]";
		mes "Apa jenis barang yang ingin Anda tukarkan?";
		mes "Untuk memeriksa informasi lebih lanjut tentang item hadiah, silakan gunakan ^3131FFKatalog^000000 kami.";
		next;
		switch(select("Weapon:Armor:Accessory:Consumable")) {
		case 1:
			mes "[Erundek]";
			mes "Anda memilih ^3131FFSenjata^000000.";
			mes "Senjata berikut tersedia untuk ditukar dengan lencana medan perang.";
			mes "Harap dicatat bahwa item untuk ^3131FFLencana Keberanian ditunjukkan sebagai (BB)^000000, dan ^3131FFLencana Valor sebagai (VB)^000000.";
			next;
			switch(select("Dagger/OneSword/TwoSword/TwoSpear:Staff/Mace/TwoAxe/Shuriken:Bow/Katar/Music/Whip:Book/Knuckle:Revolver/Rifle/Gatling/Shotgun/Launcher")) {
			case 1:
				mes "[Erundek]";
				mes "Item berikut tersedia dalam kategori ^3131FFDagger, One-Handed Sword, Two-Handed Sword, and Two-Handed Spear^000000 kategori.";
				next;
				setarray .@Weapons[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
				break;
			case 2:
				mes "[Erundek]";
				mes "Item berikut tersedia dalam kategori ^3131FFStaff / Mace / Two-Handed Axe / Huuma Shuriken^000000 kategori.";
				next;
				setarray .@Weapons[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
				break;
			case 3:
				mes "[Erundek]";
				mes "Item berikut tersedia dalam kategori ^3131FFBow / Katar / Musical Instrument / Whip^000000 kategori.";
				next;
				setarray .@Weapons[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
				break;
			case 4:
				mes "[Erundek]";
				mes "Item berikut tersedia dalam kategori ^3131FFBook / Knuckle^000000 kategori.";
				next;
				setarray .@Weapons[0],1574,7828,1575,7829,1824,7828,1823,7829;
				break;
			case 5:
				mes "[Erundek]";
				mes "Item berikut tersedia dalam kategori ^3131FFRevolver / Rifle / Gatling Gun / Shotgun / Grenade Launcher^000000 kategori.";
				next;
				setarray .@Weapons[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
				break;
			}
			set .@menu$,"";
			for(set .@i,0; .@i<getarraysize(.@Weapons); set .@i,.@i+2)
				set .@menu$, .@menu$+getitemname(.@Weapons[.@i])+((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)")+":";
			set .@i, (select(.@menu$)-1)*2;
			set .@type$, ((.@Weapons[.@i+1]==7828)?"(BB)":"(VB)");
			mes "[Erundek]";
			mes "Anda memilih ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000.";
			mes "Anda dapat menukar item ini dengan ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000.";
			mes "Mau Menukarkan?";
			next;
			switch(select("Jangan bertukar:Menukarkan")) {
			case 1:
				break;
			case 2:
				mes "[Erundek]";
				mes "Apakah Anda ingin menghabiskan ^FF0000100 "+getitemname(.@Weapons[.@i+1])+"^000000 dan menerima ^3131FF"+getitemname(.@Weapons[.@i])+.@type$+"^000000?";
				next;
				mes "[Erundek]";
				mes "Ingat, Item Hadiah Battleground adalah ^FF0000Terikat Karakter^000000. Apakah Anda yakin menginginkan barang ini?";
				next;
				switch(select("Iya:Tidak")) {
				case 1:
					mes "[Erundek]";
					if (countitem(.@Weapons[.@i+1]) >= 100) {
						mes "Terima kasih sudah bertukar.";
						delitem .@Weapons[.@i+1],100;
						getitem .@Weapons[.@i],1;
					}
					else mes "Maaf, tetapi Anda tidak memiliki cukup lencana untuk ditukar.";
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[Erundek]";
			mes "Apakah Anda perlu lebih banyak waktu untuk memeriksa item?";
			close;
		case 2:
			mes "[Erundek]";
			mes "Anda memilih ^3131FFArmor^000000.";
			mes "Armor berikut tersedia untuk ditukar dengan lencana medan perang.";
			next;
			switch(select("Garments / Shoes:Armor")) {
			case 1:
				setarray .@Items[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@Items[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "Anda memilih ^3131FFAccessory^000000.";
			mes "Anda dapat menukar Medal of Honours dengan Lencana Anda sesuai dengan kelas pekerjaan, sebagai berikut:";
			next;
			setarray .@Items[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			set .@menu1$,"Gunslinger:Swordman/Taekwon Master:Thief:Acolyte:Magician:Archer:Merchant";
			break;
		case 4:
			mes "[Erundek]";
			mes "Anda memilih ^3131FFConsumable^000000.";
			mes "Barang konsumsi berikut tersedia untuk ditukar dengan lencana medan perang:";
			next;
			setarray .@Items[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[Erundek]";
		mes "Kami memiliki banyak item, jadi silakan lihat dan beli dengan sengaja.";
		close2;
		readbook 11010,1;
		end;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@Items[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[Erundek]";
	mes "Anda memilih ^3131FF"+getitemname(.@Items[.@i])+"^000000.";
	switch(.@Items[.@i]) {
		case 2720: mes "This item is for Swordman and Taekwon Master Class only."; break;
		case 2721: mes "This item is for Thief Class only."; break;
		case 2722: mes "This item is for Acolyte Class only."; break;
		case 2723: mes "This item is for Magician Class only."; break;
		case 2724: mes "This item is for Archer Class only."; break;
		case 2725: mes "This item is for Merchant Class only."; break;
		case 2733: mes "This item is for Gunslinger only."; break;
		default: break;
	}
	mes "Anda dapat menukar item ini dengan ^FF0000"+.@Items[.@i+1]+" "+getitemname(7828)+" atau "+.@Items[.@i+1]+" "+getitemname(7829)+"^000000.";
	mes "Mau tukar tambah?";
	next;
	switch(select("Jangan bertukar:Menukarkan")) {
	case 1:
		mes "[Erundek]";
		mes "Apakah Anda perlu lebih banyak waktu untuk memeriksa barang?";
		break;
	case 2:
		mes "[Erundek]";
		mes "Lencana mana yang ingin Anda tukarkan?";
		mes "Anda membutuhkan ^3131FF"+.@Items[.@i+1]+" Lencana^000000 untuk menukarkan.";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[Erundek]";
			mes "Ingat, Item Hadiah Battleground adalah ^FF0000Terikat Karakter^000000. Apakah Anda yakin menginginkan barang ini?";
			next;
		}
		set .@j, select("Bravery Badge:Valor Badge:Cancel");
		mes "[Erundek]";
		if (.@j == 3) {
			mes "Anda membatalkan pertukaran.";
			break;
		}
		set .@cost, ((.@j==1)?7828:7829);
		if (countitem(.@cost) >= .@Items[.@i+1]) {
			mes "Terima kasih sudah bertukar.";
			delitem .@cost, .@Items[.@i+1];
			getitem .@Items[.@i],1;
		}
		else mes "Anda tidak punya cukup "+getitemname(.@cost)+"s.";
		break;
	}
	close;
}

// Badge Reward Function
//============================================================
function	script	F_BG_Badge	{
	.@team$ = getarg(1);
	.@bg_type$ = getarg(2);

	if (.@bg_type$ == "Tierra") {
		set .@badge,7828; //BF_Badge1
		set .@amount_win,3;
		set .@amount_lose,1;
	} else if (.@bg_type$ == "Flavius") {
		set .@badge,7829; //BF_Badge2
		set .@amount_win,9;
		set .@amount_lose,3;
	} else
		return;

	if (getarg(0) == 1) {
		set .@amount, .@amount_win;
		if (.@team$ == "Guillaume") {
			mes "[Axl Rose]";
			mes "Guillaume Diberkati!";
		} else if (.@team$ == "Croix") {
			mes "[Swandery]";
			mes "Croix Diberkati!";
		}
		mes "Mari kita nikmati kemenangan gemilang kita!";
		mes strcharinfo(0)+", itu adalah tanda yang mencerminkan kemenangan.";
	} else {
		set .@amount, .@amount_lose;
		if (.@team$ == "Guillaume") {
			mes "[Axl Rose]";
			mes "Kamu kalah, tapi kamu berdedikasi untuk pertempuran ini.";
			mes "Ini adalah hadiah atas dedikasi Anda yang luar biasa oleh Guillaume Marrollo!";
			mes "Anggap saja kekalahan ini sebagai pelajaran, dan lain kali kamu pasti menang.";
		} else if (.@team$ == "Croix") {
			mes "[Swandery]";
			mes "Oh, "+strcharinfo(0)+" Jangan sedih.";
			mes "Meskipun kami tidak menang, kami melakukan yang terbaik.";
			mes "Ini adalah hadiah Kerajaan dari Croix, dan tolong jangan lupakan pertempuran ini. Kita akan memenangkan pertempuran berikutnya.";
		}
	}
	close2;

	// If the VIP system is enabled, VIP players receive an extra 2 badges.
	if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
		set .@amount, .@amount+2;

	set .@medal_gap, 500 - countitem(.@badge);
	if (.@medal_gap >= .@amount)
		getitem .@badge,.@amount;
	else
		getitem .@badge,.@medal_gap;
	return;
}
