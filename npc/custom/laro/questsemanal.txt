//===== Hercules Script ======================================
//= Euphy's Quest Semanal
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6a
//===== Description: =========================================
// = A loja de busca dinâmico baseado em Lunar de, com configuração mais fácil.
// = Inclui suporte para múltiplas lojas e cashpoints.
//Pré-visualização // = item de script por ToastOfDoom.
// ===== Comentários Adicionais: =================================
// = 1.0 roteiro inicial.
// = 1.2 Adicionado suporte categoria.
// = 1.3 Mais opções e correções.
// = 1.4 definições de depuração acrescentou.
// = 1.5 Categorias substituído por IDs de loja.
// = 1.6 Adicionado suporte para a compra de Empilháveis.
// = 1.6a Adicionado suporte para visualização de trajes e roupões.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshopQ"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,156,173,4	script	Weekly Quest#1	121 ,{ 
if($language == 1) {
	mes "[Quest Semanal]";
	mes "Seja bem vindo ao LaRO";
	mes "Que tal começa voando, aqui tenho alguns itens para você:";
}else{
		mes "[Weekly Quest]";
		mes "Welcome to LaRO";
		mes "So it starts flying, here are some items for you:";
}
next;
callfunc "qshopQ";
}


// Script Core
//============================================================
-	script	quest_shopQ	-1,{
function AddQ; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Anunciam busca conclusão? (1: sim / 0: nenhum)
	set .ShowSlot,1;	// Mostrar espaços de item? (2: todos os equipamentos / 1: se ranhuras> 0/0: never)
	set .ShowID,0;  	// Mostrar IDs de item? (1: sim / 0: nenhum)
	set .ShowZeny,1;	// Mostrar custo Zeny, se houver? (1: sim / 0: nenhum)
	set .MaxStack,1;	// Número máximo de itens de quest comprado ao mesmo tempo.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	//setarray .Shops$[1],"Equipamentos para Cabeça","Weapons","Other";
		setarray .Shops$[1],"Weekly Quest";
// -----------------------------------------------------------
// Itens de missão - NÃO use um item de recompensa mais de uma vez!
// Adicionar (<loja ID>, <recompensa ID>, <quantidade recompensa>, <Custo Zeny>, <ponto de custo>,
//<ID item necessário>, <quantidade item necessário> {...});
// -----------------------------------------------------------


	  AddQ(1,5361,1,1000000,0,7216,300,2218,1);
	  //AddQ(1,18600,1,0,0,6153,800,7253,100); 


// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshopQ"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	 if($language == 1) {
		dispbottom "Selecione um item de cada vez.";
	  }else{
		dispbottom "Select one item at a time.";
	 }
	
	callshop "qshopQ"+.@i,1;
	npcshopattach "qshopQ"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	
	if($language == 1) {
		mes "[Quest Semanal]";
		mes "Recompensa: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
		mes "Requerimento:";
	  }else{
		mes "[Weekly Quest]";
		mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
		mes "Required:";
	}
	
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Buy ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ View...":"")+": ~ ^777777Cancelar^000000")) {
		case 1:
		
	if($language == 1) {
		if (@qe[0]) {
				mes "[Quest Semanal]";
				mes "Está faltando um ou mais itens.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Semanal]";
				mes "^FF0000Você precisa "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" de capacidade de peso adicional para concluir este comércio.^000000";
				close;
			}
	  }else{
		if (@qe[0]) {
				mes "[Quest Semanal]";
				mes "One or more items are missing.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Weekly Quest]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" of additional weight capacity to complete this trade.^000000";
				close;
			}
	 }	
			if (.@q[4]) Zeny -= (.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if($language == 1) {
				if (.Announce) announce "[Quest Semanal] Parabéns "+strcharinfo(0)+" adquiriu o item "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			}else{
				if (.Announce) announce "[Weekly Quest] Congratulations "+strcharinfo(0)+" purchased the item "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			}
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function AddQ {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshopQ"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^0011FF";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshopQ	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shopQ::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshopQ1	-1,909:-1
-	shop	qshopQ2	-1,909:-1
-	shop	qshopQ3	-1,909:-1
-	shop	qshopQ4	-1,909:-1
-	shop	qshopQ5	-1,909:-1