//===== rAthena Script =======================================
//= Find the Mushroom
//===== By: ==================================================
//= Mysterious
//===== Current Version: =====================================
//= 3.6a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Find the Mushroom - random amount of Mushrooms spawns in random maps.
//= Players need to find these mushrooms and kill them to gain prizes!
//===== Additional Comments: =================================
//= 3.0 Fully Functional with Rewritten script. [Mysterious]
//= 3.6a Slightly edited. [Euphy]
//============================================================

prontera,137,233,6	script	Find the Mushroom	4_F_RUSGREEN,{
// izlude,114,149,6	script	Find the Mushroom	4_F_RUSGREEN,{
	mes "[ Find The Mushroom ]";
	if (.status == 1) {
		.@count = mobcount( .event_map$, strnpcinfo(0) + "::OnMobKilled" );
		if (.@count > 0 && getgmlevel()  < .GM) {
			mes "There are " + .@count + " Mushrooms left in " + .event_map$ + "!";
			mes "Find and kill the mushrooms to gain " + getitemname(.prize) + "!";
			close;
		}
		if(getgmlevel() > .GM){
			mes "Location of the Mushrooms: ";
			setarray .@mushroom_data[0], 0;
			for( .@i = 0; .@i < getarraysize($@mobid); .@i++ )
			{
				if(unitexists ($@mobid[.@i]))
				{
					getunitdata($@mobid[.@i], .@mushroom_data);
					mes "<NAVI>Mushroom "+(.@i+1)+"<INFO>"+.event_map$+","+ .@mushroom_data[6] +","+ .@mushroom_data[7] +",0000,0</INFO></NAVI> :"+ .@mushroom_data[6] +" "+ .@mushroom_data[7];
				}
			}
			next;
			mes "[ Find The Mushroom ]";
			mes "Should I stop the event?";
			if (select("- No:- Yes") == 1) close;
			killmonsterall .event_map$;
			.status = 0;
			.event_map$ = "";
			deletearray $@mobid;
			deletearray .@mushroom_data;
			announce "The Find the Mushroom Event has ended. All the Mushrooms have been killed.",0;
		}
		close;
	}
	mes "There is no event at the moment!";
	if (.status || getgmlevel() < .GM) close;
	mes "Start the event?";
	if (select("- No:- Yes") == 1) close;
	donpcevent strnpcinfo(0) + "::OnClock2300";
	mes "[ Find The Mushroom ]";
	mes "Event started!";
	close;

OnInit:
	.prize = 8501;	// Reward item ID
	.amount = 1;	// Reward item amount
	.GM = 60;		// GM level required to access NPC
	setarray .maps_list$[0],"izlude"; // Possible maps
	end;

OnClock0100:
OnClock0300:
OnClock0500:
OnClock0700:
OnClock0900:
OnClock1100:
OnClock1300:
OnClock1500:
OnClock1700:
OnClock1900:
OnClock2100:
OnClock2300:
	if (.status) end;
	.status = 1;
	.@spawn = rand(1,10);	// How many Mushrooms should spawn?
	.event_map$ = .maps_list$[ rand(getarraysize(.maps_list$)) ];
	.@label$ = strnpcinfo(0) + "::OnMobKilled";
	// killmonster .event_map$, .@label$;
	killmonsterall .event_map$;
	monster .event_map$,0,0,"Please don't kill me!",1085, .@spawn, .@label$;
	//areamonster .event_map$,116,142,119,145,"Please don't kill me!",1085,.@spawn, .@label$;
	announce "Find the Mushroom : Total of " + .@spawn + " Mushrooms have been spawned in " + .event_map$ + "!",0;
	sleep 2500;
	announce "Find the Mushroom : Every Mushroom you kill will give you " + getitemname(.prize) + "!",0;
	setnpctimer 0; initnpctimer;
	end;


OnTimer4000:
	for( .@i = 0; .@i < getarraysize($@mobid); .@i++ )
	{
		set .@mtalk, "";
		switch(rand(25)){
			case 1: set .@mtalk, "Noo.. Don't kill me..."; break;
			case 2: set .@mtalk, "I am just a mushroom..."; break;
			case 3: set .@mtalk, "..."; break;
			case 4: set .@mtalk, "You don't see me."; break;
			case 5: set .@mtalk, "Go away!"; break;
			case 6: set .@mtalk, "No.. I don't have legs to run!"; break;
			case 7: set .@mtalk, "Take my alcohol but don't take my life!"; break;
			case 8: set .@mtalk, "Why are you doing this!"; break;
			case 9: set .@mtalk, "Why is Nubs making me talk?!"; break;
			case 10: set .@mtalk, "Why are you killing me?"; break;
			case 11: set .@mtalk, "If you kill me.. I'll ... "; break;
			case 12: set .@mtalk, "Sob.. "; break;
			case 13: set .@mtalk, "Noo.. "; break;
			default: set .@mtalk, ""; break;
		}

		if(unitexists ($@mobid[.@i])){
			if(.@mtalk)
				unittalk $@mobid[.@i], .@mtalk, bc_area;
			else
				emotion rand(1,30), $@mobid[.@i];
		}
		sleep 500;
	}
	setnpctimer 0; initnpctimer;
	end;

OnMobKilled:
	if (playerattached() == 0)
		end;
	getitem .prize, .amount;
	.@spawn = mobcount( .event_map$, strnpcinfo(0) + "::OnMobKilled" );
	if (.@spawn > 0)
		announce "[ " + strcharinfo(0) + " ] has killed a Mushroom. There are now " + .@spawn + " Mushroom(s) left.",bc_map;
	else {
		announce "The Find the Mushroom Event has ended. All the Mushrooms have been killed.",0;
		.status = 0;
		.event_map$ = "";
		deletearray $@mobid;
		deletearray .@mushroom_data;
	}
	end;
}
